-- Based on the STFT-Based Effect Template by Geraintluff

@init
function init_stft_verb_mem(freemem)
instance(
  buffer_index, buffer_length,
  fft_buffer, window_buffer, nrg,
  input_buffer, output_buffer, delay_line
)
local(MAX_FFT_SIZE)
global(srate)
(
  // Make sure we don't end up on a boundary
  freemem = ceil(freemem / 65536) * 65536;

  MAX_FFT_SIZE = 32768;
  
  /* There's some additional padding so that we don't have to range check when octaving */
  freemem = (fft_buffer = freemem) + MAX_FFT_SIZE * 4;
  freemem = (window_buffer = freemem) + MAX_FFT_SIZE * 2;
  freemem = (nrg = freemem) + MAX_FFT_SIZE * 4;
  
  buffer_length = srate;
  buffer_index = 0;
  freemem = (input_buffer = freemem) + buffer_length * 2;
  freemem = (output_buffer = freemem) + buffer_length * 2;
  freemem = (delay_line = freemem) + buffer_length * 2;
  freemem
);

function block_stft_verb()
instance(fft_buffer, fft_size, prev_fft_size, fft_interval, fft_scaling_factor, window_buffer, fft_size_index)
local(overlap_factor, i, r)
global()
(
  overlap_factor = 6;
  fft_size = 256 << fft_size_index;
  fft_interval = fft_size / overlap_factor;
  fft_scaling_factor = 1.0 / overlap_factor / fft_size;
  
  fft_size != prev_fft_size ? (
    prev_fft_size = fft_size;
    // Fill window buffer
    i = 0;
    loop(fft_size,
      r = (i + 0.5) / fft_size;
      window_buffer[i] = (0.5 - 0.5 * cos(2 * $pi * r)) / sqrt(0.375);
      i += 1;
    );
  );
);

function stft_reverb_settings(diff, decay, depth, new_pitch, lowpass, highpass, shimmer, drops, shift_mode, shift_freq)
instance(
  diffusion,
  time_constant,
  pitch,          /* Pitch */
  octaver,
  buffer_mode,
  adaptiveness,
  lp,
  hp1, hp2,
  modulation,
  frequency_shifter_mode,
  frequency_shift,
  fft_size_index,
)
global(srate, eco)
local()
(
  diffusion = diff;
  time_constant = - 2 * decay;
  octaver = shimmer;//2.5 * shimmer;
  fft_size_index = 7 - eco;
  adaptiveness = 0;
  pitch = new_pitch;
  buffer_mode = 1;
  modulation = depth;
  lp = 0.5 * srate * exp( (1 - lowpass) * log(20/22050) );
  hp1.init_tpt_monopole(0.5 * srate * exp( (1.0 - highpass) * log(20/22050) ));
  hp2.coeff = hp1.coeff;
  frequency_shifter_mode = shift_mode;
  frequency_shift = shift_freq;
);

function process_stft_segment(fft_buffer, fft_size)
global(
  srate
)
instance(
  nrg, differential, modulation,
  
  diffusion,
  time_constant,
  pitch,          /* Pitch */
  lp, hp,
  
  buffer_mode,
  adaptiveness,
)
local(
  fft_bin, tfft_bin2, a, tmp, /* Iterators */
  displacement, /* Pitch displacement handling */
  magnitude_left, magnitude_right,     /* Spectral magnitude */
  left_real, left_imag, right_real, right_imag,
  mL, mR, phaseL, phaseR,  /* Output variables */
  ts, k, one_min_k, /* Swell dynamics related */
  lp_corner,
  lp_factor,
  slope,
  mod,
  tfft_bin,
  pi_two_diff,
  
  y0, y1, y2, b1, ip, w, // Cosine filter setup
)
(
  fft_bin = 0; // FFT bin number
  mod = modulation / fft_size;
  
  ts = min(0, time_constant * (1 - adaptiveness * differential));
  k = exp(2.302585092994045 * ts);
  one_min_k = 1.0 - k;
  
  pi_two_diff = 2.0 * $pi * diffusion;

  lp_corner = 2 * fft_size * lp / srate;
  lp_factor = 1;
  slope = 1.0 - 8.0 / fft_size;
  
  /* Filter that generates a cosine */
  w = mod;
  y0 = 0;
  b1 = 2.0 * cos(w);
  ip = 0.5 * $pi;
  y1 = sin(ip - w);
  y2 = sin(ip - 2.0 * w);
  
  displacement = 2 * 2^(- pitch);
  a = 0;
  tfft_bin = 0;
  loop(fft_size/2,
    tfft_bin2 = fft_bin ? 2 * (fft_size - fft_bin) : 0;

    // Unfold complex spectrum into two real spectra
    left_real = fft_buffer[tfft_bin] + fft_buffer[tfft_bin2];
    left_imag = fft_buffer[tfft_bin + 1] - fft_buffer[tfft_bin2 + 1];
    right_real = fft_buffer[tfft_bin + 1] + fft_buffer[tfft_bin2 + 1];
    right_imag = -fft_buffer[tfft_bin] + fft_buffer[tfft_bin2];
    
    magnitude_left = 5 * sqrt(left_real * left_real + left_imag * left_imag);
    magnitude_right = 5 * sqrt(right_real * right_real + right_imag * right_imag);
 
    lp_factor *= fft_bin > lp_corner ? slope : 1;
    nrg[tfft_bin]   = max(one_min_k * nrg[tfft_bin], magnitude_left) * lp_factor;
    nrg[tfft_bin + 1] = max(one_min_k * nrg[tfft_bin + 1], magnitude_right) * lp_factor;
    
    (a < fft_size) ? (
      mL = 0.5 * nrg[a];
      mR = 0.5 * nrg[a + 1];
    ) : (
      mL = 0.5 * nrg[tfft_bin];
      mR = 0.5 * nrg[tfft_bin + 1];
    );
    
    mod ? (
      y0 = b1 * y1 - y2;
      y2 = y1;
      y1 = y0;
    
      /* Cosine modulation pattern written as filter for performance */
      tmp = y0;
      mL *= tmp;
      mR *= tmp;
    );
    
    diffusion < 0.999 ? (
      phaseL = atan2(left_imag, left_real) + pi_two_diff * rand();
      phaseR = atan2(right_imag, right_real) + pi_two_diff * rand();
    ) : (
      phaseL = 2.0 * $pi * rand();
      phaseR = 2.0 * $pi * rand();
    );
    
    left_real = cos(phaseL) * mL;
    left_imag = sin(phaseL) * mL;
    right_real = cos(phaseR) * mR;
    right_imag = sin(phaseR) * mR;
    
    // Re-fold back into complex spectrum
    fft_buffer[tfft_bin] = (left_real - right_imag) * 0.5;
    fft_buffer[tfft_bin + 1] = (left_imag + right_real) * 0.5;
    fft_buffer[tfft_bin2] = (left_real + right_imag) * 0.5;
    fft_buffer[tfft_bin2 + 1] = (-left_imag + right_real) * 0.5;

    fft_bin += 1;
    tfft_bin += 2;
    a += displacement;
  );
);

function process_stft_segment_effects(fft_buffer, fft_size)
global(
  srate,
)
instance(
  nrg, octaver, diffusion, differential,
  time_constant,
  pitch,          /* Pitch */
  adaptiveness,
  frequency_shifter_mode, frequency_shift,
)
local(
  fft_bin, a,  /* Iterators */
  g_rand1, g_rand2, g_rand3, g_rand4, tmp, /* Random numbers */
  ts, k, /* Swell dynamics related */
  current_shift, hz_shift,
)
(
  g_rand1 = rand();
  g_rand2 = g_rand1 * rand();
  g_rand3 = exp(-2.302585092994045);
  ts = min(0, time_constant * (1 - adaptiveness * differential));
  k = exp(2.302585092994045 * ts);

  octaver ? (
    fft_bin = 0; 
    tmp = octaver * k;
    a = 0;
    
    frequency_shifter_mode ? (
      // Max shift is 3520 Hz
      hz_shift = 3520 * exp( (1.0 - frequency_shift) * log(20/3520) );
      current_shift = 4 * hz_shift * fft_size / srate;
      current_shift = 2 * floor(current_shift / 2);
      
      fft_bin = current_shift + 2;
      loop(fft_size/2 - current_shift / 2 - 2,
        nrg[fft_bin] += tmp * nrg[fft_bin - current_shift];
        nrg[fft_bin + 1] += tmp * nrg[fft_bin + 1 - current_shift]; //524
        fft_bin += 2;
      );
    ) : (
      loop(fft_size/2,
        nrg[a] += tmp * nrg[fft_bin];
        nrg[a + 1] += tmp * nrg[fft_bin + 1];
        fft_bin += 2; 
        a += 4;
      );
    );
  );
  
  /* Avoid HF buildup over the edge */
  fft_bin = fft_size / 4;
  loop(fft_size/4,
    a = 4*fft_bin;
    nrg[a] -= k * nrg[a];
    nrg[a + 1] -= k * nrg[a + 1];
    fft_bin += 1;
  );
);

function fft_input(input_buffer, fft_buffer, window_buffer, fft_size, buffer_index, buffer_length, fft_scaling_factor)
local(bi, i, i2)
global()
(
  // Copy input to buffer
  bi = buffer_index - fft_size + 1;
  i = 0;
  loop(fft_size,
    i2 = bi + i;
    i2 < 0 ? i2 += buffer_length;
  
    fft_buffer[2 * i] = input_buffer[2 * i2] * window_buffer[i];
    fft_buffer[2 * i + 1] = input_buffer[2 * i2 + 1] * window_buffer[i];
  
    i += 1;
  );
  
  // Process buffer
  fft(fft_buffer, fft_size);
  fft_permute(fft_buffer, fft_size);
);


function process_stft_verb(in0, in1)
local(i, bi, i2)
global()
instance(
  buffer_ready,
  input_buffer,
  output_buffer,
  buffer_index,
  output_index,
  fft_counter, fft_interval,
  fft_buffer,
  buffer_length,
  window_buffer,
  fft_size,
  fft_scaling_factor,
  out_left,
  out_right,
  hp1, hp2,
  )
(
  input_buffer[buffer_index * 2] = in0;
  input_buffer[buffer_index * 2 + 1] = in1;
  
  buffer_ready > 0 ? (
    buffer_ready += 1;
  );
  
  // Steps 2-4 are spread over multiple blocks to prevent
  // all the CPU load from falling on a single block and leading
  // to stutter.
  (buffer_ready == 256) ? (
    this.process_stft_segment(fft_buffer, fft_size);
  ) : (buffer_ready == 512) ? (
    this.process_stft_segment_effects(fft_buffer, fft_size);
  ) : (buffer_ready == 784) ? (
    fft_ipermute(fft_buffer, fft_size);
    ifft(fft_buffer, fft_size);
  ) : (buffer_ready == 1024) ? (
    // Add to output
    bi = buffer_index - fft_size + 1;
    i = 0;
    loop(fft_size,
      i2 = bi + i;
      (i2 < 0) ? i2 += buffer_length;
      output_buffer[2 * i2] += fft_buffer[2 * i] * window_buffer[i] * fft_scaling_factor;
      output_buffer[2 * i2 + 1] += fft_buffer[2 * i + 1] * window_buffer[i] * fft_scaling_factor;
      i += 1;
    );
    buffer_ready = 0;
  );
  
  // Once a block is ready, begin processing.
  fft_counter += 1;
  fft_counter >= fft_interval ? (
    fft_counter = 0;
    fft_input(input_buffer, fft_buffer, window_buffer, fft_size, buffer_index, buffer_length, fft_scaling_factor);
    buffer_ready = 1;
  );
  
  output_index = buffer_index - fft_size;
  output_index < 0 ? output_index += buffer_length;
  
  out_left = output_buffer[output_index * 2];
  out_right = output_buffer[output_index * 2 + 1];
  
  out_left = out_left - hp1.eval_tpt_monopole(out_left);
  out_right = out_right - hp2.eval_tpt_monopole(out_right);
  
  output_buffer[output_index * 2] = 0;
  output_buffer[output_index * 2 + 1] = 0;
  
  buffer_index = (buffer_index + 1) % buffer_length;
);

