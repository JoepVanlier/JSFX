desc:Saike Morphing Filter
tags: filter non-linear
version: 0.1
author: Joep Vanlier
changelog: 
  + Initial commit
license: MIT

in_pin:left input
in_pin:right input
out_pin:left output
out_pin:right output

slider1:0<-6,48,1>Drive (dB)
slider2:0<-6,48,1>Post Boost (dB)
slider12:filter_type=1<0,1,1{Linear,MS-20}>Filter type
slider13:.6<0,1,.0001>Cutoff
slider14:0.7<0,1,.0001>Resonance
slider15:morph=0<0,1,.0001>Morph
slider6:inertia=200<0,200,.001>Inertia [ms]
slider60:oversampling=1<1,8,1>Oversampling

@init
  // MS-20 Simulation settings
  epsilon = 0.00000000001;
  epsilon = 0.00000001;
  maxiter = 6;
  safety_limit = 0.935;


function init_linearSVF(freq, res)
  global(oversampling)
  local(g)
  instance(f0, ic1eq, ic2eq, k, a1, a2, a3)
  (
    f0 = exp( (1-freq) * log(20/22050) ) / (max(1,oversampling));
    g = tan(.5 * $pi * f0);
    k = 2 - 2*res;
    
    a1 = 1/(1+g*(g+k));
    a2 = g*a1;
    a3 = g*a2;
  ); 

function reset_linearSVF()
  global()
  local()
  instance(ic1eq, ic2eq, k, a1, a2, a3)
  (
    ic1eq = ic2eq = 0;  
  );
  
function eval_linearSVF_LP(v0)
  global()
  local(v1, v2, v3)
  instance(ic1eq, ic2eq, k, a1, a2, a3)
  (
    v3 = v0 - ic2eq;
    v1 = a1 * ic1eq + a2 * v3;
    v2 = ic2eq + a2 * ic1eq + a3*v3;
    ic1eq = 2*v1 - ic1eq;
    ic2eq = 2*v2 - ic2eq;    
    
    v2
  );  
  
function eval_linearSVF_BP(v0)
  global()
  local(v1, v2, v3)
  instance(ic1eq, ic2eq, k, a1, a2, a3)
  (
    v3 = v0 - ic2eq;
    v1 = a1 * ic1eq + a2 * v3;
    v2 = ic2eq + a2 * ic1eq + a3*v3;
    ic1eq = 2*v1 - ic1eq;
    ic2eq = 2*v2 - ic2eq;    
    
    v1
  );  
  
function eval_linearSVF_HP(v0)
  global()
  local(v1, v2, v3)
  instance(ic1eq, ic2eq, k, a1, a2, a3)
  (
    v3 = v0 - ic2eq;
    v1 = a1 * ic1eq + a2 * v3;
    v2 = ic2eq + a2 * ic1eq + a3*v3;
    ic1eq = 2*v1 - ic1eq;
    ic2eq = 2*v2 - ic2eq;    
    
    v0 - k*v1 - v2
  );  
  
function eval_linearSVF_BR(v0)
  global()
  local(v1, v2, v3)
  instance(ic1eq, ic2eq, k, a1, a2, a3)
  (
    v3 = v0 - ic2eq;
    v1 = a1 * ic1eq + a2 * v3;
    v2 = ic2eq + a2 * ic1eq + a3*v3;
    ic1eq = 2*v1 - ic1eq;
    ic2eq = 2*v2 - ic2eq;    
    
    v0 - v1
  );

  function expensive_tanh(x)
  local(em2x) 
  global()
  (
    x = x;
    em2x = exp(-2*x);
    (2/(1+em2x))-1
  );
  
  // Approximate, but good enough for most things
  function tanh(s)
  local(s2, a, b, c)
  global()
  (
    s2 = s * s;
    a = s * (135135.0 + s2 * (17325.0 + s2 * (378.0 + s2)));
    b = 135135.0 + s2 * (62370.0 + s2 * (3150.0 + s2 * 28.0));
    c = a / b;
    (c < -1) ? c = -1 : (c > 1) ? c = 1 : c;
  );
  
  function f_g(s)
  local()
  global()
  instance()
  (
    max(-1,min(1,s))
  );
  
  function f_dg(s)
  local()
  global()
  instance()
  (
    1 - 1 * (abs(s)>1)
  );
  
 function smooth_parameter(target)
  instance(s, coeff)
  global()
  local(y, v)
  (
    v = coeff*(target - s);
    y = v + s;
    s = y + v;
    y
  );
  
  function initialize_smoother(cutoff)
  instance(coeff)
  global(srate)
  local(g)
  ( 
    g = tan($pi*cutoff/srate);
    coeff = g/(1+g);  
  );

  function interpolator_init(slider_idx)
  instance(next_val, idx)
  local()
  global()
  (
    next_val = slider(slider_idx);
    idx = slider_idx;
  );

  function interpolator_block()
  instance(delta, next_changepoint_t, next_val, idx)
  local(next_changepoint_y)
  global(samplesblock)
  (
    next_changepoint_t = slider_next_chg(idx, next_changepoint_y);
    next_changepoint_t > 0 ? 
    (
      next_val = slider(idx);
    ) : (
      next_changepoint_y = slider(idx);
      next_changepoint_t = samplesblock;
    );
    
    delta = (next_changepoint_y - next_val) / next_changepoint_t;
  );
  
  function interpolate()
  instance(delta, next_changepoint_t, next_val, idx, coeff)
  local(current_value, next_changepoint_y)
  global(current_sample)
  (
    current_value = next_val;
    current_sample == next_changepoint_t ? 
    (
      delta = 0;
      next_changepoint_t = slider_next_chg(idx, next_changepoint_y);
      delta = next_changepoint_t > current_sample ? (next_changepoint_y - current_value) / (next_changepoint_t-current_sample) : 0;
    );
    
    next_val = current_value + delta;
    
    coeff > 0 ? this.smooth_parameter(current_value) : current_value
  );
  
  function initialize_interpolators()
  (
    cutoff_interpolator.interpolator_init(13);
    reso_interpolator.interpolator_init(14);
    morph_interpolator.interpolator_init(15);
    drive_interpolator.interpolator_init(1);
    gain_interpolator.interpolator_init(2);
    (last_inertia != inertia) ? (
      last_inertia = inertia;
      inertia_cutoff = 1000.0/inertia;
      cutoff_interpolator.initialize_smoother(inertia_cutoff);
      reso_interpolator.initialize_smoother(inertia_cutoff);
      morph_interpolator.initialize_smoother(inertia_cutoff);
      drive_interpolator.initialize_smoother(inertia_cutoff);
      gain_interpolator.initialize_smoother(inertia_cutoff);
      cutoff_interpolator.s = slider(13);
      reso_interpolator.s = slider(14);
      morph_interpolator.s = slider(15);
      change += 1;
    );
  );
  
  initialize_interpolators();

  function init_MS20(freq, reso)
    global(oversampling, srate)
    local(f0)
    instance(y1, y2, d1, d2, h, hh, k)
    (
      f0 = exp( (1-freq) * log(20/22050) ) * $pi / oversampling;
      h = tan(f0 / (2.1 * oversampling)) * 2.1 * oversampling;
      hh = 0.5 * h;
      k  = 2*reso;
    );

  function eval_MS20_nonlin_tanh(x)
    global(epsilon, maxiter)
    local(iter, res, gd2k, ky2, gky2, dgky2,
          f1, f2, a, b, c, d, norm, sig1, thsig1, thsig1sq, sig2, thsig2, thsig2sq, tanhterm1, tanhterm2, hhthsig1sqm1, hhthsig2sqm1 )
    instance(i, y1, y2, d1, d2, h, hh, k, obs)
    (
      gd2k = f_g(d2*k);
      tanhterm1 = tanh(-d1 + x - gd2k);
      tanhterm2 = tanh(d1 - d2 + gd2k);
      
      iter = 0;
      while(
        iter += 1;
        ky2 = k*y2;
        gky2 = f_g(ky2);
        dgky2 = f_dg(ky2);
      
        sig1 = x - y1 - gky2;
        thsig1 = tanh(sig1);
        thsig1sq = thsig1 * thsig1;
      
        sig2 = y1 - y2 + gky2;
        thsig2 = tanh(sig2);
        thsig2sq = thsig2 * thsig2;
        hhthsig1sqm1 = hh*(thsig1sq - 1);
        hhthsig2sqm1 = hh*(thsig2sq - 1);
      
        f1 = y1 - d1 - hh*(tanhterm1 + thsig1);
        f2 = y2 - d2 - hh*(tanhterm2 + thsig2);
        res = abs(f1) + abs(f2);
        
        a = -hhthsig1sqm1 + 1;
        b = -k*hhthsig1sqm1*dgky2;
        c = hhthsig2sqm1;
        d = (k*dgky2 - 1)*hhthsig2sqm1 + 1;
        
        norm = 1 / ( a*d - b*c );
        y1 = y1 - ( d*f1 - b*f2 ) * norm;
        y2 = y2 - ( a*f2 - c*f1 ) * norm;
      (res > epsilon) && (iter < maxiter);
      );
      
      d1 = y1;
      d2 = y2;
    ); 


  function eval_MS20_nonlinBP_tanh(x)
    global(epsilon, maxiter)
    local(iter, res, gd2k, ky2, gky2, dgky2,
          f1, f2, a, b, c, d, norm, sig1, thsig1, thsig1sq, sig2, thsig2, thsig2sq, tanhterm1, tanhterm2, hhthsig1sqm1, hhthsig2sqm1 )
    instance(i, y1, y2, d1, d2, h, hh, k, obs)
    (
      gd2k = f_g(d2*k);
      tanhterm1 = tanh(-d1 - x - gd2k);
      tanhterm2 = tanh(d1 - d2 + x + gd2k);
      
      iter = 0;
      while(
        iter += 1;
        ky2 = k*y2;
        gky2 = f_g(ky2);
        dgky2 = f_dg(ky2);
      
        sig1 = -x - y1 - gky2;
        thsig1 = tanh(sig1);
        thsig1sq = thsig1 * thsig1;
      
        sig2 = x + y1 - y2 + gky2;
        thsig2 = tanh(sig2);
        thsig2sq = thsig2 * thsig2;
      
        hhthsig1sqm1 = hh*(thsig1sq - 1);
        hhthsig2sqm1 = hh*(thsig2sq - 1);
      
        f1 = y1 - d1 - hh*(tanhterm1 + thsig1);
        f2 = y2 - d2 - hh*(tanhterm2 + thsig2);
        res = abs(f1) + abs(f2);
        
        a = 1 - hhthsig1sqm1;
        b = -k*hhthsig1sqm1*dgky2;
        c = hhthsig2sqm1;
        d = (k*dgky2 - 1)*hhthsig2sqm1 + 1;
      
        norm = 1 / ( a*d - b*c );
        y1 = y1 - ( d*f1 - b*f2 ) * norm;
        y2 = y2 - ( a*f2 - c*f1 ) * norm;
      (res > epsilon) && (iter < maxiter);
      );
      
      d1 = y1;
      d2 = y2;
    );
    
  function eval_MS20_nonlinBR_tanh(x)
    global(epsilon, maxiter)
    local(iter, res, gd2k, ky2, gky2, dgky2,
          f1, f2, a, b, c, d, norm, sig1, thsig1, thsig1sq, sig2, thsig2, thsig2sq, tanhterm1, tanhterm2, hhthsig1sqm1, hhthsig2sqm1 )
    instance(i, y1, y2, d1, d2, h, hh, k, obs)
    (
      gd2k = f_g(d2*k);
      tanhterm1 = tanh(-d1 - x - gd2k);
      tanhterm2 = tanh(d1 - d2 + x + gd2k);
      
      iter = 0;
      while(
        iter += 1;
        ky2 = k*y2;
        gky2 = f_g(ky2);
        dgky2 = f_dg(ky2);
      
        sig1 = -x - y1 - gky2;
        thsig1 = tanh(sig1);
        thsig1sq = thsig1 * thsig1;
      
        sig2 = x + y1 - y2 + gky2;
        thsig2 = tanh(sig2);
        thsig2sq = thsig2 * thsig2;
      
        hhthsig1sqm1 = hh*(thsig1sq - 1);
        hhthsig2sqm1 = hh*(thsig2sq - 1);
      
        f1 = y1 - d1 - hh*(tanhterm1 + thsig1);
        f2 = y2 - d2 - hh*(tanhterm2 + thsig2);
        res = abs(f1) + abs(f2);
        
        a = 1 - hhthsig1sqm1;
        b = -k*hhthsig1sqm1*dgky2;
        c = hhthsig2sqm1;
        d = (k*dgky2 - 1)*hhthsig2sqm1 + 1;
      
        norm = 1 / ( a*d - b*c );
        y1 = y1 - ( d*f1 - b*f2 ) * norm;
        y2 = y2 - ( a*f2 - c*f1 ) * norm;
      (res > epsilon) && (iter < maxiter);
      );
      
      d1 = y1;
      d2 = y2;
      x - y2;
    );     
    
    function eval_MS20_nonlinHP_tanh(x)
      global(epsilon, maxiter)
      local(iter, res, gkd2px, kxpy2, gkxpy2, dgky2px,
            f1, f2, a, b, c, d, norm, sig1, thsig1, thsig1sq, sig2, thsig2, thsig2sq, tanhterm1, tanhterm2, hhthsig1sqm1, hhthsig2sqm1 )
      instance(i, y1, y2, d1, d2, h, hh, k, obs)
      (
        gkd2px = f_g(k*(d2 + x));
        tanhterm1 = tanh(-d1 - gkd2px);
        tanhterm2 = tanh(d1 - d2 - x + gkd2px);
        
        iter = 0;
        while(
          iter += 1;
          kxpy2 = k*(x + y2);
          gkxpy2 = f_g(kxpy2);
          dgky2px = f_dg(kxpy2);
        
          sig1 = -y1 - gkxpy2;
          thsig1 = tanh(sig1);
          thsig2sq = thsig2 * thsig2;
        
          sig2 = -x + y1 - y2 + gkxpy2;
          thsig2 = tanh(sig2);
          thsig2sq = thsig2 * thsig2;
        
          hhthsig1sqm1 = (thsig1sq - 1);
          hhthsig2sqm1 = (thsig2sq - 1);
        
          f1 = y1 - d1 - hh*(tanhterm1 + thsig1);
          f2 = y2 - d2 - hh*(tanhterm2 + thsig2);
          res = abs(f1) + abs(f2);
          
          a = -hhthsig1sqm1 + 1;
          b = -k*hhthsig1sqm1*dgky2px;
          c = hhthsig2sqm1;
          d = (k*dgky2px - 1)*hhthsig2sqm1 + 1;
          
          norm = 1 / ( a*d - b*c );
          y1 = y1 - ( d*f1 - b*f2 ) * norm;
          y2 = y2 - ( a*f2 - c*f1 ) * norm;
        (res > epsilon) && (iter < maxiter);
        );
          
        d1 = y1;
        d2 = y2;
        
        y2 + x
      ); 

  // UP / DOWNSAMPLING
  // Generate windowed sinc filter at memory location FIR
  // Inputs are:
  //    fir   - Memory location to store windowed sinc
  //    nt    - Number of taps
  //    bw    - Fractional bandwidth
  //     g    - Gain
  function sinc(fir, nt, bw, g)
    local(a, ys, yg, yw, i, pibw2, pifc2, pidnt2, hnt)
    global()
    (
      pibw2   = 2.0*$pi*bw;
      pidnt2  = 2.0*$pi/nt;
      hnt     = 0.5*nt;
      i       = 1;
          
      loop(nt-1,
        // Sinc width
        a  = (i - hnt) * pibw2;
          
        // Sinc
        ys = (a != 0) ? sin(a)/a : 1.0;
   
        // Window gain
        yg = g * (4.0 * bw);
          
        // Hamming window (could be replaced with Kaiser in the future)
        yw = 0.54 - 0.46 * cos(i * pidnt2);
           
        // Calc FIR coeffs
        fir[i-1] = yw * yg * ys;
        
        i += 1;
      );
    );
  
  // Generate sinc filters for a specific upsampling ratio
  //
  // Upsampling leads to a sample followed by N-1 zeroes. Hence 
  // to compute each subsample, we only need 1/Nth of the taps.
  // This is why we set up a specific filter for each subsample.
  // i.e. for N=4, you get something like f1*Zn + f5*Zn-1 + ...
  //
  // Inputs:
  //    N_in            - oversampling factor
  //    tapsPerFilter   - Taps per subfilter (should be 8 in this implementation)
  //    targetmem       - Location to store the coefficients
  //    tmp             - Working memory
  function updateSincFilter(N_in, tapsPerFilter, targetmem, tmp)
    local(nHist, iFilt, nTaps)
    instance(h0, h1, h2, h3, h4, h5, h6, coeffs, loc, N, delta)
    global()
    (
      N       = N_in;
      nHist   = tapsPerFilter;
      loc     = 0;
      coeffs  = targetmem;
      nTaps   = N*nHist;
      
      // Memory being set is conservatively large.
      memset(coeffs,0,10000);
      memset(tmp,0,10000);
      
      sinc(tmp, nTaps, .5/N, .5*N);
      
      // Divide sinc over the different filters
      iFilt = 0; // Filter idx for which subsample this filter is
      delta = 0; // Sample idx
      loop(nTaps,
        coeffs[delta + iFilt*100] = tmp[];
        iFilt += 1;
        iFilt == N ? ( iFilt = 0; delta += 1 );
        tmp += 1;
      );
    );
  
  // Generate downsample filter
  // Here, the full N*nHist tap filter has to be evaluated for every sample, 
  // but only every Nth sample has to be evaluated.
  function updateSincDownsampleFilter(N_in, nTaps_in, histmem, coeffmem)
    global()
    instance(hist, hend, hptr, coeffs, loc, N, delta, nTaps)
    local()
    (
      N       = N_in;
      hist    = histmem;
      coeffs  = coeffmem;
      nTaps   = nTaps_in;
      hptr    = hist;
      hend    = hist + nTaps;
      
      memset(coeffs,0,10000);
      sinc(coeffs, nTaps, .5/N, .5);
    );
    
  function advanceHist(sample)
    global()
    instance(hist, hptr, hend, coeffs, loc, N, delta, nTaps)
    local(nHist, nTaps)
    (
      hptr += 1;
      ( hptr == hend ) ? hptr = hist;
      hptr[] = sample;
    );
  
  function sincDownSample()
    global()
    instance(hist, hptr, hend, coeffs, loc, N, delta, nTaps)
    local(nHist, hm1, hptr2, out, cfptr)
    (
      hm1   = hist-1;
      hptr2 = hptr;
      cfptr = coeffs;
      out   = 0;
      
      loop(nTaps,
        out = out + hptr2[] * cfptr[];
        
        cfptr += 1;
        hptr2 -= 1;
        ( hptr2 == hm1 ) ? hptr2 = hend-1;
      );
      
      out
    );
  
  function resetSincDown()
    global()
    instance(hist, hptr, hend, coeffs, loc, N, delta, nTaps)
    local(nHist, hm1, hptr2)
    (
      hm1   = hist-1;
      hptr2 = hptr;
      
      loop(nTaps,
        hptr2[] = 0;
        
        hptr2 -= 1;
        ( hptr2 == hm1 ) ? hptr2 = hend-1;
      );
    );
  
  // Maintain input sample history. Hardcoded for speed.
  // Note h7 is omitted because for integer upsampling it is always zero!
  function advanceSinc(sample)
    instance(h0, h1, h2, h3, h4, h5, h6, coeffs, loc, N)
    global()
    local(filt)
    (
      h6 = h5;
      h5 = h4;
      h4 = h3;
      h3 = h2;
      h2 = h1;
      h1 = h0;
      h0 = sample;
      loc = 0;
    );
  
  function resetSincUp()
    instance(h0, h1, h2, h3, h4, h5, h6, coeffs, loc, N)
    global()
    local(filt)
    (
      h0 = h1 = h2 = h3 = h4 = h5 = h6 = 0;
    );
  
  // Note h7 is omitted because for integer upsampling it is always zero!
  function getSubSample()
    instance(h0, h1, h2, h3, h4, h5, h6, coeffs, loc, N)
    global()
    local(filt, out)
    (
      filt = coeffs + loc;
      out =  filt[] * h0 + filt[1] * h1 + filt[2] * h2 + filt[3] * h3 + filt[4] * h4 + filt[5] * h5 + filt[6] * h6;
  
      loc += 100;
      out
    );

  function inputFilter(sample)
    instance(len, d1, d2, d3, d4, o1, o2, o3, o4, a1, a2, b0, b1, b2)
    local (out)
    global ()
    (
      out  = sample*b0 + d1*b1 + d2*b2 - d3*a1 - d4*a2;
      d2   = d1;
      d1   = sample;
      d4   = d3;
      d3   = out;
    );
  
  function outputFilter(sample)
    instance(len, d1, d2, d3, d4, o1, o2, o3, o4, a1, a2, b0, b1, b2)
    local (out)
    global ()
    (
      out = sample*b0 + o1*b1 + o2*b2 - o3*a1 - o4*a2;
      o2  = o1;
      o1  = sample;
      o4  = o3;
      o3  = out;
    );

bpos=0;

cutoff_mem  = 0;
reso_mem    = 10000;
sinc_hist1  = 20000;
sinc_hist2  = 30000;
sinc_flt    = 40000;
sinc_flt2   = 50000;
sinc_flt3   = 60000;
sinc_flt4   = 70000;
sinc_tmp    = 80000;

@slider
initialize_interpolators();

(filter_type == 1) || (oversampling == 1) ? (
  slider13 > safety_limit ? (
    slider_automate(slider13 = safety_limit);
    warning = 75;
  );
);

@block
    cutoff_interpolator.interpolator_block();
    reso_interpolator.interpolator_block();
    morph_interpolator.interpolator_block();
    drive_interpolator.interpolator_block();
    gain_interpolator.interpolator_block();

   // Update the oversampling filters when needed.
   ( oversampling != lastOversample ) ? 
   (
     lastOversample = oversampling;   
  
     // Memory for the sincs is located at sinc_tmp, sinc_flt, sinc_flt2, sinc_flt3 and sinc_flt4
     sincFilterL.updateSincFilter(oversampling, 8, sinc_flt,  sinc_tmp);
     sincFilterR.updateSincFilter(oversampling, 8, sinc_flt2, sinc_tmp);
     nTapSinc = oversampling < 5 ? 16 : oversampling*4;
     sincDownL.updateSincDownsampleFilter(oversampling, nTapSinc, sinc_hist1, sinc_flt3);
     sincDownR.updateSincDownsampleFilter(oversampling, nTapSinc, sinc_hist2, sinc_flt4);  
  );

@sample 
 function processSample_ms20(s, choice)
 local()
 instance(BP, HP, BR, LP)
 global()
  (
    (choice == 1) ? (
      s = BP.eval_MS20_nonlinBP_tanh(s)  
    ) : (choice == 2) ? (
      s = HP.eval_MS20_nonlinHP_tanh(s)
    ) : (choice == 3) ? (
      s = BR.eval_MS20_nonlinBR_tanh(s)
    ) : (
      // LP is last and first
      s = LP.eval_MS20_nonlin_tanh(s)
    );
  );
  
 function processSample_lin(s, choice)
 local()
 instance(BP, HP, BR, LP)
 global()
  (
    (choice == 1) ? (
      s = BP.eval_linearSVF_BP(s)  
    ) : (choice == 2) ? (
      s = HP.eval_linearSVF_HP(s)
    ) : (choice == 3) ? (
      s = BR.eval_linearSVF_BR(s)
    ) : (
      // LP is last and first
      s = LP.eval_linearSVF_LP(s)
    );
  );  

  log10d20_conversion  = .11512925464970228420089957273422; //log(10)/20
  preamp      = exp(log10d20_conversion*drive_interpolator.interpolate());
  inv_preamp  = exp(-log10d20_conversion*drive_interpolator.interpolate() + log10d20_conversion*gain_interpolator.interpolate());

  spl0 *= preamp;
  spl1 *= preamp;
   
  sliderCutoff = cutoff_interpolator.interpolate();
  sliderReso = reso_interpolator.interpolate();
  sliderMorph = morph_interpolator.interpolate();
  
  filter_type == 0 ? (
    L.LP.init_linearSVF(sliderCutoff, sliderReso);
    R.HP.a1 = L.HP.a1 = R.BR.a1 = L.BR.a1 = R.BP.a1 = L.BP.a1 = R.LP.a1 = L.LP.a1;
    R.HP.a2 = L.HP.a2 = R.BR.a2 = L.BR.a2 = R.BP.a2 = L.BP.a2 = R.LP.a2 = L.LP.a2;
    R.HP.a3 = L.HP.a3 = R.BR.a3 = L.BR.a3 = R.BP.a3 = L.BP.a3 = R.LP.a3 = L.LP.a3;
    R.HP.k = L.HP.k = R.BR.k = L.BR.k = R.BP.k = L.BP.k = R.LP.k = L.LP.k;
  ) : (
    (oversampling == 1) ? (
      sliderCutoff = sliderCutoff > safety_limit ? safety_limit : sliderCutoff;
    );
  
    L.LP.init_MS20(sliderCutoff, sliderReso);
    R.HP.hh = L.HP.hh = R.BR.hh = L.BR.hh = R.BP.hh = L.BP.hh = R.LP.hh = L.LP.hh;
    R.HP.h = L.HP.h = R.BR.h = L.BR.h = R.BP.h = L.BP.h = R.LP.h = L.LP.h;
    R.HP.k = L.HP.k = R.BR.k = L.BR.k = R.BP.k = L.BP.k = R.LP.k = L.LP.k;
  );
  
  filter_choice = floor(sliderMorph*4);
  frac_morph = sliderMorph*4 - filter_choice;
  i_morph = 1.0 - frac_morph;
  
  // MS-20
  ( oversampling > 1 ) ? (
    sincFilterL.advanceSinc(spl0);
    sincFilterR.advanceSinc(spl1);
    
    loop( oversampling,
      ssl = sincFilterL.getSubSample();
      ssr = sincFilterR.getSubSample();
      
      filter_type == 0 ? (
        ssl = L.processSample_lin(ssl, filter_choice) * i_morph + L.processSample_lin(ssl, filter_choice + 1) * frac_morph;
        ssr = R.processSample_lin(ssr, filter_choice) * i_morph + R.processSample_lin(ssr, filter_choice + 1) * frac_morph;
      ) : (
        ssl = L.processSample_ms20(ssl, filter_choice) * i_morph + L.processSample_ms20(ssl, filter_choice + 1) * frac_morph;
        ssr = R.processSample_ms20(ssr, filter_choice) * i_morph + R.processSample_ms20(ssr, filter_choice + 1) * frac_morph;
      );
      
      sincDownL.advanceHist(ssl);
      sincDownR.advanceHist(ssr);
    );
    
    spl0 = sincDownL.sincDownSample();
    spl1 = sincDownR.sincDownSample();
  ) : (
    filter_type == 0 ? (
      spl0 = L.processSample_lin(spl0, filter_choice) * i_morph + L.processSample_lin(spl0, filter_choice + 1) * frac_morph;
      spl1 = R.processSample_lin(spl1, filter_choice) * i_morph + R.processSample_lin(spl1, filter_choice + 1) * frac_morph;
    ) : (
      spl0 = L.processSample_ms20(spl0, filter_choice) * i_morph + L.processSample_ms20(spl0, filter_choice + 1) * frac_morph;
      spl1 = R.processSample_ms20(spl1, filter_choice) * i_morph + R.processSample_ms20(spl1, filter_choice + 1) * frac_morph;
    );
  );
  
  spl0 *= inv_preamp;
  spl1 *= inv_preamp;
  
@gfx
xs = 10;
ys = 10;

gfx_set(0, 0, 0, 0);
gfx_rect(0, 0, gfx_w, gfx_h);
gfx_set(1, 1, 1, 1);
gfx_x = xs;
gfx_y = ys;
gfx_printf("Cutoff: %d Hz", .5 * srate * exp( (1-slider13) * log(20/22050) ) );
dx = 40;

gfx_x = xs;
gfx_y = ys + 25;
gfx_printf("LP");

gfx_x = xs+dx;
gfx_y = ys + 25;
gfx_printf("BP");

gfx_x = xs+2*dx;
gfx_y = ys + 25;
gfx_printf("HP");

gfx_x = xs+3*dx;
gfx_y = ys + 25;
gfx_printf("BR");

gfx_x = xs+4*dx;
gfx_y = ys + 25;
gfx_printf("LP");

gfx_y = ys + 40;
gfx_line(xs, gfx_y, xs+4*dx, gfx_y);

xc = xs + 4*morph*dx;
gfx_line(xc, gfx_y - 5, xc, gfx_y + 5);

gfx_x = 0;
gfx_y = ys + 50;

warning > 0 ? (
  gfx_x = 0;
  gfx_y += 25;
  gfx_a = warning / 75;
  gfx_printf("Warning! Without oversampling, the non-linear filter cannot safely go higher.");
  warning -= 1;
);

