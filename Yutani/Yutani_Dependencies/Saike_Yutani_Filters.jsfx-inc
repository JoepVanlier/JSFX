@init
// MS-20 Simulation settings
epsilon = 0.00000001;
//epsilon = 0.01;
maxiter = 6;
safety_limit_ms20 = 0.935;
safety_limit_moog = 0.25; // Above this level the model suffers from numerical difficulties. Ensures fc < fs/8.

current_safety_moog = (1.0 - log(safety_limit_moog * oversampling) / log(20/22050));

  function tanh(x)
  local()
  global()
  instance()
  (
    2/(1+exp(-2*x)) - 1
  );

function check_safety()
(
  ((filter_type == 1) || (filter_type==6)) && (oversampling == 1) ? (
    cutoff > safety_limit_ms20 ? (
      slider_automate(cutoff = safety_limit_ms20);
      warning = 75;
    );
  );
  
  (filter_type == 3) || (filter_type == 4) ?
  (
    cutoff > current_safety_moog ? (
      slider_automate(cutoff = current_safety_moog);
      warning = 75;
    );
  );
);

function init_303(cutoff, q)
  local(dwc, dwc2, dwc3, qwc2, qwc3)
  global(oversampling)
  instance(wc, wc2, wc3, wc4, A, k, b, g, z0, z1, z2, z3, y1, y2, y3, y4,
  b0, a0, a1, a2, a3, b10, a10, a11, a12, a13, b20, a20, a21, a22, a23, c2, c3, sc)
  (
    // This specific filter came from
    // Copyright (c) 2012 Dominique Wurtz (www.blaukraut.info)
    wc = exp( (1-cutoff) * log(20/22050) ) / oversampling;
    wc = tan(.39 * $pi * wc);
    
    wc2 = wc*wc;
    wc3 = wc2*wc;
    wc4 = wc3*wc;
    b   = 1.0 / ( 1.0 + 8.0*wc + 20.0*wc2 + 16.0*wc3 + 2.0*wc4);
    g   = 2.0 * wc4 * b;
    
    k   = 16.95*q;
    A   = 1 + 0.5 * k;
    
    dwc = 2*wc;
    dwc2 = 2*wc2;
    qwc2 = 4*wc2;
    dwc3 = 2*wc3;
    qwc3 = 4*wc3;
    
    b0 = dwc+12*wc2+20*wc3+8*wc4;
    a0 = 1+6*wc+10*wc2+qwc3;
    a1 = dwc+8*wc2+6*wc3;
    a2 = dwc2+wc3;
    a3 = dwc3;
    
    b10 = dwc2+8*wc3+6*wc4;
    a10 = wc+4*wc2+3*wc3;
    a11 = 1+6*wc+11*wc2+6*wc3;
    a12 = wc+qwc2+qwc3;
    a13 = wc2+dwc3;
    
    b20 = dwc3+4*wc4;
    a20 = a13;
    a21 = wc+qwc2+4*wc3;
    a22 = 1+6*wc+10*wc2+qwc3;
    a23 = wc+qwc2+dwc3;
    
    c2  = a21 - a3;
    c3  = 1+6*wc+9*wc2+dwc3;
    
    sc = (57.96533646143774 - 26.63612328945456*exp(- 0.44872755850609214 * k)) / 31.329213171983177;
  ); 
  
function reset_303()
  local(Kbig)
  global(slider54)
  instance(wc, wc2, wc3, wc4, A, k, b, g, z0, z1, z2, z3, y1, y2, y3, y4,
  b0, a0, a1, a2, a3, b10, a10, a11, a12, a13, b20, a20, a21, a22, a23, c2, c3 )
  (
    z1 = z2 = z3 = 0;
    y1 = y2 = y3 = y4 = 0;
  );
  
function eval_303(input, choice, frac)
  local(y0, s0, s, f1, f2, fb)
  instance(wc, wc2, wc3, wc4, A, k, b, g, z0, z1, z2, z3, y1, y2, y3, y4
            b0, a0, a1, a2, a3, b10, a10, a11, a12, a13, b20, a20, a21, a22, a23, c2, c3, sc)
  global()
  (
    s = (z0*wc3 + z1*a20 + z2*c2 + z3*c3) * b;
    y4 = (g*input + s) / (1.0 + g*k);
    
    fb = input - k*y4;
    y0 = max(-1,min(1,fb));
 
    y1 = b * (y0*b0 + z0*a0 + z1*a1 + z2*a2 + z3*a3);     // #OK
    y2 = b * (y0*b10 + z0*a10 + z1*a11 + z2*a12 + z3*a13);  // #OK
    y3 = b * (y0*b20 + z0*a20 + z1*a21 + z2*a22 + z3*a23);  //
    y4 = ((g*y0 + s));
    
    z0 += 4*wc*(y0 - y1   + y2);
    z1 += 2*wc*(y1 - 2*y2 + y3);
    z2 += 2*wc*(y2 - 2*y3 + y4);
    z3 += 2*wc*(y3 - 2*y4);
    
    (choice == 0 ) ? (
      f1 = A*y4; // LP
      f2 = y4 + y2 - y1; // BP
    ) : (choice == 1) ? (
      f1 = y4 + y2 - y1; // BP
      f2 = -(y0 - y4)*.5; // HP
    ) : (choice == 2) ? (
      frac = frac * frac;
      frac = frac * frac;
      f1 = -(y0 - y4)*.5; // HP
      f2 = -(y0*2 + (y4 + y2 - y1))*.5; // BR
    ) : (choice == 3) ? (
      f1 = (y0*2 + (y4 + y2 - y1))*.5; // BR
      f2 = A*y4; // LP  
    );
    
    (f2 * frac + f1 * (1.0-frac))*sc
);

function init_linearSVF(freq, res)
  global(oversampling)
  local(g)
  instance(f0, ic1eq, ic2eq, k, a1, a2, a3)
  (
    f0 = exp( (1-freq) * log(20/22050) ) / oversampling;
    g = tan(.5 * $pi * f0);
    k = 2 - 2*res;
        
    a1 = 1/(1+g*(g+k));
    a2 = g*a1;
    a3 = g*a2;
  ); 
  
function init_linearSVF_absolute(f0, res)
  global(srate)
  local(g)
  instance(f0, ic1eq, ic2eq, k, a1, a2, a3)
  (
    g = tan($pi * f0 / srate);
    k = 2 - 2*res;
        
    a1 = 1/(1+g*(g+k));
    a2 = g*a1;
    a3 = g*a2;
  ); 

function reset_linearSVF()
  global()
  local()
  instance(ic1eq, ic2eq, k, a1, a2, a3)
  (
    ic1eq = ic2eq = 0;  
  );
  
function eval_linearSVF_LP(v0)
  global()
  local(v1, v2, v3)
  instance(ic1eq, ic2eq, a1, a2, a3)
  (
    v3 = v0 - ic2eq;
    v1 = a1 * ic1eq + a2 * v3;
    v2 = ic2eq + a2 * ic1eq + a3*v3;
    ic1eq = 2*v1 - ic1eq;
    ic2eq = 2*v2 - ic2eq;    
    
    v2
  );  
  
function eval_linearSVF_BP(v0)
  global()
  local(v1, v2, v3)
  instance(ic1eq, ic2eq, k, a1, a2, a3)
  (
    v3 = v0 - ic2eq;
    v1 = a1 * ic1eq + a2 * v3;
    v2 = ic2eq + a2 * ic1eq + a3*v3;
    ic1eq = 2*v1 - ic1eq;
    ic2eq = 2*v2 - ic2eq;    
    
    v1
  );  
  
function eval_linearSVF_HP(v0)
  global()
  local(v1, v2, v3)
  instance(ic1eq, ic2eq, k, a1, a2, a3)
  (
    v3 = v0 - ic2eq;
    v1 = a1 * ic1eq + a2 * v3;
    v2 = ic2eq + a2 * ic1eq + a3*v3;
    ic1eq = 2*v1 - ic1eq;
    ic2eq = 2*v2 - ic2eq;    
    
    v0 - k*v1 - v2
  );  
  
function eval_linearSVF_BR(v0)
  global()
  local(v1, v2, v3)
  instance(ic1eq, ic2eq, k, a1, a2, a3)
  (
    v3 = v0 - ic2eq;
    v1 = a1 * ic1eq + a2 * v3;
    v2 = ic2eq + a2 * ic1eq + a3*v3;
    ic1eq = 2*v1 - ic1eq;
    ic2eq = 2*v2 - ic2eq;    
    
    v0 - v1
  );

  function expensive_tanh(x)
  local(em2x) 
  global()
  (
    x = x;
    em2x = exp(-2*x);
    (2/(1+em2x))-1
  );
  
  function init_moog2(cutoff, resonance)
  local(fs, fc, g, p0s, nmp, gN, kgN, p0g, tmp, acc)
  global(srate, oversampling)
  instance(VT2, rg1, rg2, qg1, qg2, si1, sf1, sg1, si2, sf2, sg2, k0g, k0s, VT2i, q0s, r1s, k)
  (
    k    = resonance*120;

    fs   = srate * oversampling;
    fc   = .5 * srate * exp( (1-cutoff) * log(20/22050) );    
    g    = tan($pi / fs * fc) / sqrt(1 + k);
    
    VT2  = 0.052;
    VT2i = 19.23076923076923;
  
    // Ladder stages
    p0s = 1.0 / (1.0 + g);
    q0s = 1.0 - g;
    r1s = -g;
    k0s = VT2 * g * p0s;
  
    // Global filter
    nmp = (1.0 - p0s);
    gN  = nmp ^ 2;
    kgN = k * gN;
    p0g = 1.0 / (1.0 + kgN);
    k0g = -VT2i * p0g;
  
    rg1 = -2.0*kgN;
    rg2 = -1.0*kgN;
    acc = tmp = p0s*(g - 1.0);
    qg1 = -2.0*(kgN + acc);
    acc = acc*tmp;
    qg2 = -1.0*(kgN + acc);
  );
  
  function eval_moog2(x)
  local(yi, yd, yf)
  global()
  instance(yo, y, A, B, rg1, rg2, qg1, qg2, si1, sf1, sg1, si2, sf2, sg2, k0g, k0s, VT2i, q0s, r1s, k)
  (
    yo = tanh(k0g * (x + sg1));
    A    = yo;
  
    yi   = yo;
    yd   = k0s * (yi + sf1);
    y    = yd + si1;
    yo   = tanh(VT2i * y);
    B    = yo;
  
    si1 = yd + y;
    sf1 = r1s * yi - q0s * yo;
  
    yi   = yo;
    yd   = k0s * (yi + sf2);
    y    = yd + si2;
    yo   = tanh(VT2i * y);
  
    si2 = yd + y;
    sf2 = r1s * yi - q0s * yo;
  
    yf = k * y;
  
    sg1 = rg1 * x + qg1 * yf + sg2;
    sg2 = rg2 * x + qg2 * yf;
  );  
  
  function mix_moog2(x, choice, frac)
  local()
  global()
  instance(VT, VT2, VT2i, A, B, y, yo, k, f1, f2)
  (
    this.eval_moog2(x*VT2);
    
    (choice == 0) ? (
      f1 = y * (1 + k); // LP
      f2 = VT2 * (2*B - 2*yo)*8; // BP
    ) : (choice == 1) ? (
      f1 = VT2 * (2*B - 2*yo)*8; // BP
      f2 = VT2 * (A - B); // HP
    ) : (choice == 2) ? (
      frac = frac * frac;
      frac = frac * frac;
      f1 = VT2 * (A - B); // HP
      f2 = -VT2 * (2*B-2*yo-A); // BR
    ) : (choice == 3) ? (
      f1 = -VT2 * (2*B-2*yo-A); // BR
      f2 = y * (1 + k); // LP
    );
    
    (f2 * frac + f1 * (1.0-frac))*VT2i;
  );
  
  function init_moog(cutoff, resonance)
  local(fs, fc, g, p0s, nmp, gN, kgN, p0g, tmp, acc)
  global(srate, oversampling)
  instance(VT2, rg1, rg2, rg3, rg4, qg1, qg2, qg3, qg4, si1, sf1, sg1, si2, sf2, sg2, si3, sf3, sg3, si4, sf4, sg4, k0g, k0s, VT2i, q0s, r1s, k)
  (
    k    = resonance* 3.9999999999999987;
    fc   = .5 * srate * exp( (1-cutoff) * log(20/22050) );
    fs   = srate * oversampling;
    g    = tan($pi * fc / fs) / sqrt(1.0 + sqrt(k) - 2 * pow(k, 0.25) * 0.7071067811865476);
    VT2  = 0.052;
    VT2i = 19.23076923076923;
  
    // Ladder stages
    p0s = 1.0 / (1.0 + g);
    q0s = 1.0 - g;
    r1s = -g;
    k0s = VT2 * g * p0s;
  
    // Global filter
    nmp = (1.0 - p0s);
    gN  = nmp * nmp * nmp * nmp;
    kgN = k * gN;
    p0g = 1.0 / (1.0 + kgN);
    k0g = -VT2i * p0g;
    
    rg1 = -4.0*kgN;
    rg2 = -6.0*kgN;
    rg3 = -4.0*kgN;
    rg4 = -1.0*kgN;
    acc = tmp = p0s*(g - 1.0);
    qg1 = -4.0*(kgN + acc);
    acc = acc*tmp;
    qg2 = -6.0*(kgN + acc);
    acc = acc*tmp;
    qg3 = -4.0*(kgN + acc);
    acc = acc*tmp;
    qg4 = -1.0*(kgN + acc);
  );
  
  function eval_moog(x)
  local(yi, yd, yf)
  global()
  instance(rg1, rg2, rg3, rg4, qg1, qg2, qg3, qg4, si1, sf1, sg1, si2, sf2, sg2, si3, sf3, sg3, si4, sf4, sg4, k0g, k0s, VT2i, q0s, r1s, k,
           A, B, C, D, y, yo)
  (
    yo = tanh(k0g * (x + sg1));
    A = yo;
  
    yi   = yo;
    yd   = k0s * (yi + sf1);
    y    = yd + si1;
    yo   = tanh(VT2i * y);
    B = yo;
  
    si1 = yd + y;
    sf1 = r1s * yi - q0s * yo;
  
    yi   = yo;
    yd   = k0s * (yi + sf2);
    y    = yd + si2;
    yo   = tanh(VT2i * y);
    C = yo;
  
    si2 = yd + y;
    sf2 = r1s * yi - q0s * yo;
  
    yi   = yo;
    yd   = k0s * (yi + sf3);
    y    = yd + si3;
    yo   = tanh(VT2i * y);
    D = yo;
  
    si3 = yd + y;
    sf3 = r1s * yi - q0s * yo;
  
    yi   = yo;
    yd   = k0s * (yi + sf4);
    y    = yd + si4;
    yo   = tanh(VT2i * y);
  
    si4 = yd + y;
    sf4 = r1s * yi - q0s * yo;
    yf = k * y;
  
    sg1 = rg1 * x + qg1 * yf + sg2;
    sg2 = rg2 * x + qg2 * yf + sg3;
    sg3 = rg3 * x + qg3 * yf + sg4;
    sg4 = rg4 * x + qg4 * yf;
  );  
  
  function mix_moog(x, choice, frac)
  local()
  global()
  instance(VT, VT2, VT2i, A, B, C, D, y, yo, k, f1, f2)
  (
    this.eval_moog(x*VT2);
    
    (choice == 0) ? (
      f1 = y * (1 + k); // LP
      f2 = VT2 * (2*C - 2*B); // BP
    ) : (choice == 1) ? (
      f1 = VT2 * (2*C - 2*B); // BP
      //f1 = VT2 * (4*C - 8*B + 4*yo)/6; // BP
      f2 = VT2 * (A - 4 * B + 6 * C - 4 * D + yo); // HP
    ) : (choice == 2) ? (
      frac = frac * frac; /* Make sure the HP gets some space */
      frac = frac * frac;
      f1 = VT2 * (A - 4 * B + 6 * C - 4 * D + yo); // HP
      f2 = VT2 * (A - 4 * B + 6 * C - 4 * D); // Notch
    ) : (choice == 3) ? (
      f1 = VT2 * (A - 4 * B + 6 * C - 4 * D); // Notch
      f2 = y * (1 + k); // LP
    );
    
    (f2 * frac + f1 * (1.0-frac))*VT2i
  );  
  
  function f_g_asym(s)
  local()
  global()
  instance()
  (
    s > 0 ? max(-1,min(1,s))
    : max(-1,min(1,s*.25))
  );
  
  function f_g(s)
  local()
  global()
  instance()
  (
    max(-1,min(1,s))
  );
  
  function f_dg_asym(s)
  local()
  global()
  instance()
  (
    s > 0 ? 1 - 1 * (abs(s)>1)
    : .25 - .25 * (abs(s)>4)
  );  
  
  function f_dg(s)
  local()
  global()
  instance()
  (
    1 - 1 * (abs(s)>1)
  );
  
  function init_MS20(freq, reso)
    global(oversampling, srate)
    local(f0)
    instance(y1, y2, d1, d2, h, hh, k)
    (
      f0 = exp( (1-freq) * log(20/22050) ) * $pi / oversampling;
      h = tan(f0 / (2.1 * oversampling)) * 2.1 * oversampling;
      hh = 0.5 * h;
      k  = 2*reso;
    );
    
  function eval_MS20_nonlin_tanh(x)
    global(epsilon, maxiter)
    local(iter, res, gd2k, ky2, gky2, dgky2,
          f1, f2, a, b, c, d, norm, sig1, thsig1, thsig1sq, sig2, thsig2, thsig2sq, tanhterm1, tanhterm2, hhthsig1sqm1, hhthsig2sqm1 )
    instance(i, y1, y2, d1, d2, h, hh, k, obs)
    (
      gd2k = f_g(d2*k);
      tanhterm1 = tanh(-d1 + x - gd2k);
      tanhterm2 = tanh(d1 - d2 + gd2k);
      
      iter = 0;
      while(
        iter += 1;
        ky2 = k*y2;
        gky2 = f_g(ky2);
        dgky2 = f_dg(ky2);
      
        sig1 = x - y1 - gky2;
        thsig1 = tanh(sig1);
        thsig1sq = thsig1 * thsig1;
      
        sig2 = y1 - y2 + gky2;
        thsig2 = tanh(sig2);
        thsig2sq = thsig2 * thsig2;
        hhthsig1sqm1 = hh*(thsig1sq - 1);
        hhthsig2sqm1 = hh*(thsig2sq - 1);
      
        f1 = y1 - d1 - hh*(tanhterm1 + thsig1);
        f2 = y2 - d2 - hh*(tanhterm2 + thsig2);
        res = abs(f1) + abs(f2);
        
        a = -hhthsig1sqm1 + 1;
        b = -k*hhthsig1sqm1*dgky2;
        c = hhthsig2sqm1;
        d = (k*dgky2 - 1)*hhthsig2sqm1 + 1;
        
        norm = 1 / ( a*d - b*c );
        y1 = y1 - ( d*f1 - b*f2 ) * norm;
        y2 = y2 - ( a*f2 - c*f1 ) * norm;
      (res > epsilon) && (iter < maxiter);
      );
      
      d1 = y1;
      d2 = y2;
    ); 


  function eval_MS20_nonlinBP_tanh(x)
    global(epsilon, maxiter)
    local(iter, res, gd2k, ky2, gky2, dgky2, kc,
          f1, f2, a, b, c, d, norm, sig1, thsig1, thsig1sq, sig2, thsig2, thsig2sq, tanhterm1, tanhterm2, hhthsig1sqm1, hhthsig2sqm1 )
    instance(i, y1, y2, d1, d2, h, hh, k, obs)
    (
      kc = .95*k;
      gd2k = f_g(d2*kc);
      tanhterm1 = tanh(-d1 - x - gd2k);
      tanhterm2 = tanh(d1 - d2 + x + gd2k);
      
      iter = 0;
      while(
        iter += 1;
        ky2 = kc*y2;
        gky2 = f_g(ky2);
        dgky2 = f_dg(ky2);
      
        sig1 = -x - y1 - gky2;
        thsig1 = tanh(sig1);
        thsig1sq = thsig1 * thsig1;
      
        sig2 = x + y1 - y2 + gky2;
        thsig2 = tanh(sig2);
        thsig2sq = thsig2 * thsig2;
      
        hhthsig1sqm1 = hh*(thsig1sq - 1);
        hhthsig2sqm1 = hh*(thsig2sq - 1);
      
        f1 = y1 - d1 - hh*(tanhterm1 + thsig1);
        f2 = y2 - d2 - hh*(tanhterm2 + thsig2);
        res = abs(f1) + abs(f2);
        
        a = 1 - hhthsig1sqm1;
        b = -kc*hhthsig1sqm1*dgky2;
        c = hhthsig2sqm1;
        d = (kc*dgky2 - 1)*hhthsig2sqm1 + 1;
      
        norm = 1 / ( a*d - b*c );
        y1 = y1 - ( d*f1 - b*f2 ) * norm;
        y2 = y2 - ( a*f2 - c*f1 ) * norm;
      (res > epsilon) && (iter < maxiter);
      );
      
      d1 = y1;
      d2 = y2;
    );
    
  function eval_MS20_nonlinBR_tanh(x)
    global(epsilon, maxiter)
    local(iter, res, gd2k, ky2, gky2, dgky2,
          f1, f2, a, b, c, d, norm, sig1, thsig1, thsig1sq, sig2, thsig2, thsig2sq, tanhterm1, tanhterm2, hhthsig1sqm1, hhthsig2sqm1 )
    instance(i, y1, y2, d1, d2, h, hh, k, obs)
    (
      gd2k = f_g(d2*k);
      tanhterm1 = tanh(-d1 - x - gd2k);
      tanhterm2 = tanh(d1 - d2 + x + gd2k);
      
      iter = 0;
      while(
        iter += 1;
        ky2 = k*y2;
        gky2 = f_g(ky2);
        dgky2 = f_dg(ky2);
      
        sig1 = -x - y1 - gky2;
        thsig1 = tanh(sig1);
        thsig1sq = thsig1 * thsig1;
      
        sig2 = x + y1 - y2 + gky2;
        thsig2 = tanh(sig2);
        thsig2sq = thsig2 * thsig2;
      
        hhthsig1sqm1 = hh*(thsig1sq - 1);
        hhthsig2sqm1 = hh*(thsig2sq - 1);
      
        f1 = y1 - d1 - hh*(tanhterm1 + thsig1);
        f2 = y2 - d2 - hh*(tanhterm2 + thsig2);
        res = abs(f1) + abs(f2);
        
        a = 1 - hhthsig1sqm1;
        b = -k*hhthsig1sqm1*dgky2;
        c = hhthsig2sqm1;
        d = (k*dgky2 - 1)*hhthsig2sqm1 + 1;
      
        norm = 1 / ( a*d - b*c );
        y1 = y1 - ( d*f1 - b*f2 ) * norm;
        y2 = y2 - ( a*f2 - c*f1 ) * norm;
      (res > epsilon) && (iter < maxiter);
      );
      
      d1 = y1;
      d2 = y2;
      x - y2;
    );     
    
    function eval_MS20_nonlinHP_tanh(x)
      global(epsilon, maxiter)
      local(iter, res, gkd2px, kxpy2, gkxpy2, dgky2px, kc,
            f1, f2, a, b, c, d, norm, sig1, thsig1, thsig1sq, sig2, thsig2, thsig2sq, tanhterm1, tanhterm2, hhthsig1sqm1, hhthsig2sqm1 )
      instance(i, y1, y2, d1, d2, h, hh, k, obs)
      (
        kc = .85*k;
        gkd2px = f_g(kc*(d2 + x));
        tanhterm1 = tanh(-d1 - gkd2px);
        tanhterm2 = tanh(d1 - d2 - x + gkd2px);
        
        iter = 0;
        while(
          iter += 1;
          kxpy2 = kc*(x + y2);
          gkxpy2 = f_g(kxpy2);
          dgky2px = f_dg(kxpy2);
        
          sig1 = -y1 - gkxpy2;
          thsig1 = tanh(sig1);
          thsig1sq = thsig1 * thsig1;
        
          sig2 = -x + y1 - y2 + gkxpy2;
          thsig2 = tanh(sig2);
          thsig2sq = thsig2 * thsig2;
        
          hhthsig1sqm1 = (thsig1sq - 1);
          hhthsig2sqm1 = (thsig2sq - 1);
        
          f1 = y1 - d1 - hh*(tanhterm1 + thsig1);
          f2 = y2 - d2 - hh*(tanhterm2 + thsig2);
          res = abs(f1) + abs(f2);
          
          a = -hhthsig1sqm1 + 1;
          b = -kc*hhthsig1sqm1*dgky2px;
          c = hhthsig2sqm1;
          d = (kc*dgky2px - 1)*hhthsig2sqm1 + 1;
          
          norm = 1 / ( a*d - b*c );
          y1 = y1 - ( d*f1 - b*f2 ) * norm;
          y2 = y2 - ( a*f2 - c*f1 ) * norm;
        (res > epsilon) && (iter < maxiter);
        );
          
        d1 = y1;
        d2 = y2;
        
        y2 + x
      );
      
  function eval_MS20_nonlin_tanh_asym(x)
    global(epsilon, maxiter)
    local(iter, res, gd2k, ky2, gky2, dgky2, qq,
          f1, f2, a, b, c, d, norm, sig1, thsig1, thsig1sq, sig2, thsig2, thsig2sq, tanhterm1, tanhterm2, hhthsig1sqm1, hhthsig2sqm1 )
    instance(i, y1, y2, d1, d2, h, hh, k, obs)
    (
      gd2k = f_g_asym(d2*k);
      sig1 = -d1 + x - gd2k;
      qq = sig1 < 0 ? .6 : 1.0;
      sig1 *= qq;
      tanhterm1 = tanh(sig1);
      tanhterm2 = tanh(d1 - d2 + gd2k);
 
      iter = 0;
      while(
        iter += 1;
        ky2 = k*y2;
        gky2 = f_g_asym(ky2);
        dgky2 = f_dg_asym(ky2);
      
        sig1 = x - y1 - gky2;
        qq = sig1 < 0 ? .6 : 1.0;
        sig1 *= qq;
        thsig1 = tanh(sig1);
        thsig1sq = thsig1 * thsig1;
        
        sig2 = y1 - y2 + gky2;
        thsig2 = tanh(sig2);
        thsig2sq = thsig2 * thsig2;
        hhthsig1sqm1 = hh*(thsig1sq - 1);
        hhthsig2sqm1 = hh*(thsig2sq - 1);
      
        f1 = y1 - d1 - hh*(tanhterm1 + thsig1);
        f2 = y2 - d2 - hh*(tanhterm2 + thsig2);
        res = abs(f1) + abs(f2);
        
        a = -qq*hhthsig1sqm1 + 1;
        b = -qq*k*hhthsig1sqm1*dgky2;
        c = hhthsig2sqm1;
        d = (k*dgky2 - 1)*hhthsig2sqm1 + 1;
        
        norm = 1 / ( a*d - b*c );
        y1 = y1 - ( d*f1 - b*f2 ) * norm;
        y2 = y2 - ( a*f2 - c*f1 ) * norm;
      (res > epsilon) && (iter < maxiter);
      );
      
      d1 = y1;
      d2 = y2;
    ); 


  function eval_MS20_nonlinBP_tanh_asym(x)
    global(epsilon, maxiter)
    local(iter, res, gd2k, ky2, gky2, dgky2, kc, qq,
          f1, f2, a, b, c, d, norm, sig1, thsig1, thsig1sq, sig2, thsig2, thsig2sq, tanhterm1, tanhterm2, hhthsig1sqm1, hhthsig2sqm1 )
    instance(i, y1, y2, d1, d2, h, hh, k, obs)
    (
      kc = k;
      gd2k = f_g_asym(d2*kc);
      sig1 = -d1 - x - gd2k;
      qq = sig1 < 0 ? .6 : 1.0;
      sig1 *= qq;
      tanhterm1 = tanh(sig1);
      tanhterm2 = tanh(d1 - d2 + x + gd2k);
      
      iter = 0;
      while(
        iter += 1;
        ky2 = kc*y2;
        gky2 = f_g_asym(ky2);
        dgky2 = f_dg_asym(ky2);
      
        sig1 = -x - y1 - gky2;
        qq = sig1 < 0 ? .8 : 1.0;
        sig1 *= qq;
        thsig1 = tanh(sig1);
        thsig1sq = thsig1 * thsig1;      
      
        sig2 = x + y1 - y2 + gky2;
        thsig2 = tanh(sig2);
        thsig2sq = thsig2 * thsig2;
      
        hhthsig1sqm1 = hh*(thsig1sq - 1);
        hhthsig2sqm1 = hh*(thsig2sq - 1);
      
        f1 = y1 - d1 - hh*(tanhterm1 + thsig1);
        f2 = y2 - d2 - hh*(tanhterm2 + thsig2);
        res = abs(f1) + abs(f2);
        
        a = 1 - qq*hhthsig1sqm1;
        b = -qq*kc*hhthsig1sqm1*dgky2;
        c = hhthsig2sqm1;
        d = (kc*dgky2 - 1)*hhthsig2sqm1 + 1;
      
        norm = 1 / ( a*d - b*c );
        y1 = y1 - ( d*f1 - b*f2 ) * norm;
        y2 = y2 - ( a*f2 - c*f1 ) * norm;
      (res > epsilon) && (iter < maxiter);
      );
      
      d1 = y1;
      d2 = y2;
    );
    
  function eval_MS20_nonlinBR_tanh_asym(x)
    global(epsilon, maxiter)
    local(iter, res, gd2k, ky2, gky2, dgky2,
          f1, f2, a, b, c, d, norm, sig1, thsig1, thsig1sq, sig2, thsig2, thsig2sq, tanhterm1, tanhterm2, hhthsig1sqm1, hhthsig2sqm1 )
    instance(i, y1, y2, d1, d2, h, hh, k, obs)
    (
      gd2k = f_g_asym(d2*k);
      tanhterm1 = tanh(-d1 - x - gd2k);
      tanhterm2 = tanh(d1 - d2 + x + gd2k);
      
      iter = 0;
      while(
        iter += 1;
        ky2 = k*y2;
        gky2 = f_g_asym(ky2);
        dgky2 = f_dg_asym(ky2);
      
        sig1 = -x - y1 - gky2;
        thsig1 = tanh(sig1);
        thsig1sq = thsig1 * thsig1;
      
        sig2 = x + y1 - y2 + gky2;
        thsig2 = tanh(sig2);
        thsig2sq = thsig2 * thsig2;
      
        hhthsig1sqm1 = hh*(thsig1sq - 1);
        hhthsig2sqm1 = hh*(thsig2sq - 1);
      
        f1 = y1 - d1 - hh*(tanhterm1 + thsig1);
        f2 = y2 - d2 - hh*(tanhterm2 + thsig2);
        res = abs(f1) + abs(f2);
        
        a = 1 - hhthsig1sqm1;
        b = -k*hhthsig1sqm1*dgky2;
        c = hhthsig2sqm1;
        d = (k*dgky2 - 1)*hhthsig2sqm1 + 1;
      
        norm = 1 / ( a*d - b*c );
        y1 = y1 - ( d*f1 - b*f2 ) * norm;
        y2 = y2 - ( a*f2 - c*f1 ) * norm;
      (res > epsilon) && (iter < maxiter);
      );
      
      d1 = y1;
      d2 = y2;
      x - y2;
    );     
    
    function eval_MS20_nonlinHP_tanh_asym(x)
      global(epsilon, maxiter)
      local(iter, res, gkd2px, kxpy2, gkxpy2, dgky2px, kc,
            f1, f2, a, b, c, d, norm, sig1, thsig1, thsig1sq, sig2, thsig2, thsig2sq, tanhterm1, tanhterm2, hhthsig1sqm1, hhthsig2sqm1 )
      instance(i, y1, y2, d1, d2, h, hh, k, obs)
      (
        kc = k;
        gkd2px = f_g_asym(kc*(d2 + x));
        tanhterm1 = tanh(-d1 - gkd2px);
        tanhterm2 = tanh(d1 - d2 - x + gkd2px);
        
        iter = 0;
        while(
          iter += 1;
          kxpy2 = kc*(x + y2);
          gkxpy2 = f_g_asym(kxpy2);
          dgky2px = f_dg_asym(kxpy2);
        
          sig1 = -y1 - gkxpy2;
          thsig1 = tanh(sig1);
          thsig1sq = thsig1 * thsig1;
        
          sig2 = -x + y1 - y2 + gkxpy2;
          thsig2 = tanh(sig2);
          thsig2sq = thsig2 * thsig2;
        
          hhthsig1sqm1 = (thsig1sq - 1);
          hhthsig2sqm1 = (thsig2sq - 1);
        
          f1 = y1 - d1 - hh*(tanhterm1 + thsig1);
          f2 = y2 - d2 - hh*(tanhterm2 + thsig2);
          res = abs(f1) + abs(f2);
          
          a = -hhthsig1sqm1 + 1;
          b = -kc*hhthsig1sqm1*dgky2px;
          c = hhthsig2sqm1;
          d = (kc*dgky2px - 1)*hhthsig2sqm1 + 1;
          
          norm = 1.0 / ( a*d - b*c );
          y1 = y1 - ( d*f1 - b*f2 ) * norm;
          y2 = y2 - ( a*f2 - c*f1 ) * norm;
        (res > epsilon) && (iter < maxiter);
        );
          
        d1 = y1;
        d2 = y2;
        
        y2 + x
      );      
      
 function processSample_ms20(s, choice)
 local()
 instance(BP, HP, BR, LP)
 global()
  (
    (choice == 1) ? (
      BP.eval_MS20_nonlinBP_tanh(s)  
    ) : (choice == 2) ? (
      HP.eval_MS20_nonlinHP_tanh(s)
    ) : (choice == 3) ? (
      BR.eval_MS20_nonlinBR_tanh(s)
    ) : (
      // LP is last and first
      LP.eval_MS20_nonlin_tanh(s)
    );
  );
  
 function processSample_ms20_asym(s, choice)
 local()
 instance(BP, HP, BR, LP)
 global()
  (
    (choice == 1) ? (
      BP.eval_MS20_nonlinBP_tanh_asym(s)  
    ) : (choice == 2) ? (
      HP.eval_MS20_nonlinHP_tanh_asym(s)
    ) : (choice == 3) ? (
      BR.eval_MS20_nonlinBR_tanh_asym(s)
    ) : (
      // LP is last and first
      LP.eval_MS20_nonlin_tanh_asym(s)
    );
  );  
  
 function processSample_lin(s, choice)
 local()
 instance(BP, HP, BR, LP)
 global()
  (
    (choice == 1) ? (
      s = BP.eval_linearSVF_BP(s)  
    ) : (choice == 2) ? (
      s = HP.eval_linearSVF_HP(s)
    ) : (choice == 3) ? (
      s = BR.eval_linearSVF_BR(s)
    ) : (
      // LP is last and first
      s = LP.eval_linearSVF_LP(s)
    );
  );
  
function clamp(v, lb, ub)
(
  min(ub, max(v, lb))
);

function cl01(v)
(
  min(1, max(0, v))
);
  
function init_filter(filter_type, sliderCutoffL, sliderCutoffR, sliderMorphL, sliderMorphR, sliderReso)
local()
instance(
  L, R, L2, R2,
  filter_choiceL, frac_morphL, i_morphL,
  filter_choiceR, frac_morphR, i_morphR,
)
global(oversampling, safety_limit_ms20, safety_limit_moog, current_safety_moog)
(
  filter_type == 0 ? (
    L.LP.init_linearSVF(sliderCutoffL, sliderReso);
    L.HP.a1 = L.BR.a1 = L.BP.a1 = L.LP.a1;
    L.HP.a2 = L.BR.a2 = L.BP.a2 = L.LP.a2;
    L.HP.a3 = L.BR.a3 = L.BP.a3 = L.LP.a3;
    L.HP.k  = L.BR.k  = L.BP.k  = L.LP.k;
    
    R.LP.init_linearSVF(sliderCutoffR, sliderReso);
    R.HP.a1 = R.BR.a1 = R.BP.a1 = R.LP.a1;
    R.HP.a2 = R.BR.a2 = R.BP.a2 = R.LP.a2;
    R.HP.a3 = R.BR.a3 = R.BP.a3 = R.LP.a3;
    R.HP.k  = R.BR.k  = R.BP.k  = R.LP.k;
  ) : ( filter_type == 1 ) ? (
    (oversampling == 1) ? (
      sliderCutoffL = sliderCutoffL > safety_limit_ms20 ? safety_limit_ms20 : sliderCutoffL;
      sliderCutoffR = sliderCutoffR > safety_limit_ms20 ? safety_limit_ms20 : sliderCutoffR;
    );
  
    L.LP.init_MS20(sliderCutoffL, sliderReso);
    L.HP.hh = L.BR.hh = L.BP.hh = L.LP.hh;
    L.HP.h = L.BR.h = L.BP.h = L.LP.h;
    L.HP.k = L.BR.k = L.BP.k = L.LP.k;
    
    R.LP.init_MS20(sliderCutoffR, sliderReso);
    R.HP.hh = R.BR.hh = R.BP.hh = R.LP.hh;
    R.HP.h = R.BR.h = R.BP.h = R.LP.h;
    R.HP.k = R.BR.k = R.BP.k = R.LP.k;
  ) : ( filter_type == 2 ) ? (
    L.LP.init_linearSVF(sliderCutoffL, sliderReso);
    L2.HP.a1 = L2.BR.a1 = L2.BP.a1 = L2.LP.a1 = L.HP.a1 = L.BR.a1 = L.BP.a1 = L.LP.a1;
    L2.HP.a2 = L2.BR.a2 = L2.BP.a2 = L2.LP.a2 = L.HP.a2 = L.BR.a2 = L.BP.a2 = L.LP.a2;
    L2.HP.a3 = L2.BR.a3 = L2.BP.a3 = L2.LP.a3 = L.HP.a3 = L.BR.a3 = L.BP.a3 = L.LP.a3;
    L2.HP.k  = L2.BR.k  = L2.BP.k  = L2.LP.k  = L.HP.k  = L.BR.k  = L.BP.k  = L.LP.k;
    
    R.LP.init_linearSVF(sliderCutoffR, sliderReso);
    R2.HP.a1 = R2.BR.a1 = R2.BP.a1 = R2.LP.a1 = R.HP.a1 = R.BR.a1 = R.BP.a1 = R.LP.a1;
    R2.HP.a2 = R2.BR.a2 = R2.BP.a2 = R2.LP.a2 = R.HP.a2 = R.BR.a2 = R.BP.a2 = R.LP.a2;
    R2.HP.a3 = R2.BR.a3 = R2.BP.a3 = R2.LP.a3 = R.HP.a3 = R.BR.a3 = R.BP.a3 = R.LP.a3;
    R2.HP.k  = R2.BR.k  = R2.BP.k  = R2.LP.k  = R.HP.k  = R.BR.k  = R.BP.k  = R.LP.k;
  ) : ( filter_type == 3 ) ? (
    sliderCutoffL = sliderCutoffL > current_safety_moog ? current_safety_moog : sliderCutoffL;
    sliderCutoffR = sliderCutoffR > current_safety_moog ? current_safety_moog : sliderCutoffR;
  
    L.moog.init_moog(sliderCutoffL, sliderReso);
    R.moog.init_moog(sliderCutoffR, sliderReso);
  ) : ( filter_type == 4 ) ? (
    sliderCutoffL = sliderCutoffL > current_safety_moog ? current_safety_moog : sliderCutoffL;
    sliderCutoffR = sliderCutoffR > current_safety_moog ? current_safety_moog : sliderCutoffR;
    
    L.moog2.init_moog2(sliderCutoffL, sliderReso);
    R.moog2.init_moog2(sliderCutoffR, sliderReso);
  ) : ( filter_type == 5 ) ? (
    L.b303.init_303(sliderCutoffL, sliderReso);
    R.b303.init_303(sliderCutoffR, sliderReso);    
  ) : ( filter_type == 6 ) ? (
    (oversampling == 1) ? (
      sliderCutoffL = sliderCutoffL > safety_limit_ms20 ? safety_limit_ms20 : sliderCutoffL;
      sliderCutoffR = sliderCutoffR > safety_limit_ms20 ? safety_limit_ms20 : sliderCutoffR;
    );
  
    L.LP.init_MS20(sliderCutoffL, sliderReso);
    L.HP.hh = L.BR.hh = L.BP.hh = L.LP.hh;
    L.HP.h = L.BR.h = L.BP.h = L.LP.h;
    L.HP.k = L.BR.k = L.BP.k = L.LP.k;
    
    R.LP.init_MS20(sliderCutoffR, sliderReso);
    R.HP.hh = R.BR.hh = R.BP.hh = R.LP.hh;
    R.HP.h = R.BR.h = R.BP.h = R.LP.h;
    R.HP.k = R.BR.k = R.BP.k = R.LP.k;
  );
  
  filter_choiceL = floor(sliderMorphL*4);
  frac_morphL = sliderMorphL*4 - filter_choiceL;
  i_morphL = 1.0 - frac_morphL;
  
  filter_choiceR = floor(sliderMorphR*4);
  frac_morphR = sliderMorphR*4 - filter_choiceR;
  i_morphR = 1.0 - frac_morphR;
);

function processSample(filter_type)
local(csl, csr)
instance(
  L, R, L2, R2,
  filter_choiceL, frac_morphL, i_morphL,
  filter_choiceR, frac_morphR, i_morphR,
)
global(ssl, ssr, epsilon)
(
  filter_type == 0 ? (
    ssl = L.processSample_lin(ssl, filter_choiceL) * i_morphL + L.processSample_lin(ssl, filter_choiceL + 1) * frac_morphL;
    ssr = R.processSample_lin(ssr, filter_choiceR) * i_morphR + R.processSample_lin(ssr, filter_choiceR + 1) * frac_morphR;
  ) : ( filter_type == 1 ) ? (
    csl = ssl;
    csr = ssr;
    ssl = L.processSample_ms20(csl, filter_choiceL) * i_morphL;
    ssr = R.processSample_ms20(csr, filter_choiceR) * i_morphR;
    frac_morphL > epsilon ? ssl += L.processSample_ms20(csl, filter_choiceL + 1) * frac_morphL;
    frac_morphL > epsilon ? ssr += R.processSample_ms20(csr, filter_choiceR + 1) * frac_morphR;
  ) : ( filter_type == 2 ) ? (
    ssl = L.processSample_lin(ssl, filter_choiceL) * i_morphL + L.processSample_lin(ssl, filter_choiceL + 1) * frac_morphL;
    ssr = R.processSample_lin(ssr, filter_choiceR) * i_morphR + R.processSample_lin(ssr, filter_choiceR + 1) * frac_morphR;
    ssl = L2.processSample_lin(ssl, filter_choiceL) * i_morphL + L2.processSample_lin(ssl, filter_choiceL + 1) * frac_morphL;
    ssr = R2.processSample_lin(ssr, filter_choiceR) * i_morphR + R2.processSample_lin(ssr, filter_choiceR + 1) * frac_morphR;
  ) : ( filter_type == 3 ) ? (
    ssl = L.moog.mix_moog(ssl, filter_choiceL, frac_morphL);
    ssr = R.moog.mix_moog(ssr, filter_choiceR, frac_morphR);
  ) : ( filter_type == 4 ) ? (
    ssl = L.moog2.mix_moog2(ssl, filter_choiceL, frac_morphL);
    ssr = R.moog2.mix_moog2(ssr, filter_choiceR, frac_morphR);
  ) : ( filter_type == 5 ) ? (
    ssl = L.b303.eval_303(ssl, filter_choiceL, frac_morphL);
    ssr = R.b303.eval_303(ssr, filter_choiceR, frac_morphR);
  ) : ( filter_type == 6 ) ? (
    csl = ssl;
    csr = ssr;
    ssl = L.processSample_ms20_asym(csl, filter_choiceL) * i_morphL;
    ssr = R.processSample_ms20_asym(csr, filter_choiceR) * i_morphR;
    frac_morphL > epsilon ? ssl += L.processSample_ms20_asym(csl, filter_choiceL + 1) * frac_morphL;
    frac_morphR > epsilon ? ssr += R.processSample_ms20_asym(csr, filter_choiceR + 1) * frac_morphR;
  );
);

