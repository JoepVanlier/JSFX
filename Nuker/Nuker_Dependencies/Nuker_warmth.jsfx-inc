@init
function initTilt(freq, res, A)
  global()
  local(g, k)
  instance(ic1eq, ic2eq, A, a1, a2, a3, m0, m1, m2)
  (
    g = tan(.5 * $pi * freq) / sqrt(A);
    k = 2 - 2*res;
      
    a1 = 1/(1+g*(g+k));
    a2 = g*a1;
    a3 = g*a2;
      
    m0 = 1;
    m1 = k*(A-1);
    m2 = (A*A - 1);
  );

function evalTilt(v0)
  global()
  local(v1, v2, v3)
  instance(ic1eq, ic2eq, a1, a2, a3, m0, m1, m2)
  (
    v3 = v0 - ic2eq;
    v1 = a1 * ic1eq + a2 * v3;
    v2 = ic2eq + a2 * ic1eq + a3*v3;
    ic1eq = 2*v1 - ic1eq;
    ic2eq = 2*v2 - ic2eq;    
      
    (v0 + m1 * v1 + m2 * v2)
  );

function initWarmthFilters(warmth)
  local(tilt_gain, inverse_tilt_gain, omega_tilt)
  (
    tilt_gain = 10^(warmth / 20);
    inverse_tilt_gain = 1.0 / tilt_gain;
    omega_tilt = 200 / srate;
    lTilt.initTilt(omega_tilt, 0, tilt_gain);
    lInvTilt.initTilt(omega_tilt, 0, inverse_tilt_gain);
    rTilt.initTilt(omega_tilt, 0, tilt_gain);
    rInvTilt.initTilt(omega_tilt, 0, inverse_tilt_gain);
  );

