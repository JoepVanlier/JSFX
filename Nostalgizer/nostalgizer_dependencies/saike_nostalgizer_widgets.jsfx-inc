@init

// Available Functions:
//   circ.fancyCircle(x1, y1, rin, mute, solo, bypass)
//    Generates and draws circle widget
//   butt.drawButton( X, Y, W, H, string, fontindex, taper )
//     Draws button with optional tapering
//   butt.button_processMouse(mousex, mousey, mousecap)
//    Processes mouse events for button. Returns clicked.
//    knob.drawKnob(_x, _y, _r, text, _hint)
//    Draws knob and generates instance
//   knob.knob_processMouse(mousex, mousey, mouse_cap)
//    Generates a knob
//    Fields: value, value2, value3
//    User has control over field value. Others must be set by code.
//    Returns value when changed.
//   simpleSlider.drawSimpleSlider(_x, _y, _w, _h, r, g, b, a, _hint)
//   simpleSlider.simpleSlider_processMouse(mx, my, mc, default)
//    hinter.updateHintTime(hintString)
//    queue a hint...
//   hinter.drawHint_draw()
//    call this at the end of gfx

function handleModifier(str, x, y, w, h, modifierID)
instance()
local(fact, ww, hh, r, g, b, r2, g2, b2, clicked, in_range, cy)
global(KNOB_FONT, edge_r, edge_g, edge_b, edge_a, edge_font_r, edge_font_g, edge_font_b,
       gfx_x, gfx_y, render_time, mouse_x, mouse_y, mouse_cap, style,
       activeModifier, lcap)
(
  style == 0 ? (
    clicked = lcap == 0 && mouse_cap == 1;
    in_range = mouse_x > x && mouse_x < (x+w) && mouse_y > y && mouse_y < (y+h);
    
    cy = 1.0;
    activeModifier == modifierID ? (
      cy = .3 + .2*sin(4*render_time);
      r = 37/255;
      g = 236/255;
      b = 117/255;
      fact = 1.3;
      r2 = fact * edge_r;
      g2 = fact * edge_b;
      b2 = fact * edge_b;
      
      clicked && in_range ? activeModifier = 0;
    ) : (
      fact = 1.6;
      r = fact * edge_r;
      g = fact * edge_b;
      b = fact * edge_b;
      fact = 1.3;
      r2 = fact * edge_r;
      g2 = fact * edge_b;
      b2 = fact * edge_b;
      
      clicked && in_range ? activeModifier = modifierID;
    );
    gfx_set(0, 0, 0, .4);
    gfx_rect(x+5, y+5, w, h, 12, 1);
    
    
    gfx_set(r, g, b, cy);
    gfx_rect(x, y, w, h, 12, 1);
    fact = 1.3;
    gfx_set(r2, g2, b2, cy);
    gfx_rect(x+3, y+3, w-6, h-6, 8, 1);
    
    gfx_set(edge_r, edge_g, edge_b, edge_a);
    gfx_setfont(KNOB_FONT);
    gfx_measurestr(str, ww, hh);
    gfx_x = x + (w-ww)*.5;
    gfx_y = y + (h-hh)*.5;
    gfx_set(edge_font_r, edge_font_g, edge_font_b, 1.0);
    gfx_printf(str);
  ) : ( style == 1 ) ? (
    y -= 10;
    clicked = lcap == 0 && mouse_cap == 1;
    in_range = mouse_x > x && mouse_x < (x+w) && mouse_y > y && mouse_y < (y+h);
    
    cy = 1.0;
    activeModifier == modifierID ? (
      cy = .3 + .2*sin(4*render_time);
      r = 37/255;
      g = 236/255;
      b = 117/255;
      fact = 1.3;
      r2 = fact * edge_r;
      g2 = fact * edge_b;
      b2 = fact * edge_b;
      
      clicked && in_range ? activeModifier = 0;
    ) : (
      fact = 1.6;
      r = fact * edge_r;
      g = fact * edge_b;
      b = fact * edge_b;
      fact = 1.3;
      r2 = fact * edge_r;
      g2 = fact * edge_b;
      b2 = fact * edge_b;
      
      clicked && in_range ? activeModifier = modifierID;
    );
    
    gfx_set(r, g, b, cy);
    gfx_rect(x, y, w, h, 12, 1);
    fact = 1.3;
    gfx_set(r2, g2, b2, cy);
    gfx_rect(x+1, y+1, w-2, h-2, 8, 1);
    
    gfx_set(edge_r, edge_g, edge_b, edge_a);
    gfx_setfont(KNOB_FONT);
    gfx_measurestr(str, ww, hh);
    gfx_x = x + (w-ww)*.5;
    gfx_y = y + (h-hh)*.5;
    gfx_set(edge_font_r, edge_font_g, edge_font_b, 1.0);
    gfx_printf(str);  
  );
);

function clamp(value, mini, maxi)
  local()
  global()
  (
    max(min(value,maxi),mini)
  );

  /* Draw a vertical label */
  function drawVertLabel(x, y, stridx, labelW, font, labelBuffer, font_color_r, font_color_g, font_color_b, font_color_a, bottom_aligned)
  local(oldDest, oldMode, strw, strh)
  global(gfx_dest, gfx_mode, gfx_x, gfx_y, gfx_a)
  instance()
  (
    oldDest = gfx_dest;
    oldMode = gfx_mode;
    gfx_setimgdim(labelBuffer, labelW, labelW);
    gfx_dest = labelBuffer;
        
    gfx_setfont(font);
    gfx_measurestr(stridx, strw, strh);
    gfx_set(0,0,0,1);
    gfx_rect(0,0, labelW, strh);
    gfx_set(font_color_r, font_color_g, font_color_b, font_color_a);
    gfx_x = .5 * labelW - .5 * strw; gfx_y = 0;
    gfx_drawstr(stridx);
    
    gfx_a = .35;
    gfx_x = .5 * labelW - .5 * strw + 2;
    gfx_y += 1;
    gfx_drawstr(stridx);
    gfx_dest = oldDest;
    
    gfx_x = x - bottom_aligned * strh;
    gfx_y = y;
    gfx_mode = 1;
    gfx_blit(labelBuffer, 1, 1.5*$pi);
    gfx_mode = oldMode;
  ); 
  

/*-----------*/
/* HINTS     */  
/*-----------*/ 
function updateHintTime(hint)
  global(gfx_x, gfx_y, mouse_x, mouse_y)
  local()
  instance(lx, ly, hintTime, currentHint, delta_time)
  (
    ( ( abs(lx - mouse_x) + abs( ly - mouse_y ) ) > 0 ) ? (
      hintTime = 0;
    ) : (      
      (hint != 0) ? (
        currentHint = hint;
        hintTime = hintTime + delta_time;
        hintTime = min(1, hintTime)
      ) : (
        0
      )
    );
    
    lx = mouse_x;
    ly = mouse_y;
  );   
  
function drawHint_draw()
  global(scaling, gfx_x, gfx_y, gfx_w, gfx_h, mouse_x, mouse_y, HINT_FONT)
  local(w, h, globalTime)
  instance(hintTime, currentHint, lastGlobalTime, delta_time)
  (
    globalTime = time_precise();
    delta_time = globalTime - lastGlobalTime;
    lastGlobalTime = globalTime;
  
    ( hintTime > .99 ) ? (
      gfx_setfont(HINT_FONT);
      gfx_measurestr(currentHint,w,h);
      
      gfx_x = mouse_x+15;
      gfx_y = mouse_y+15;
      ( gfx_x > 0.5*gfx_w ) ? gfx_x = mouse_x - w - 8;
      ( gfx_y > 0.5*gfx_h ) ? gfx_y = mouse_y - h - 8;

      gfx_set( 0.05, 0.05, 0.1, .8 );
      gfx_rect(gfx_x-2, gfx_y-2, w+4, h+4);
      gfx_set( .7, .7, .7, 1 );      
      gfx_printf(currentHint);
    );
  );    
  
  
/*------------------*/
/* Slider widget   */
/*------------------*/  
function sliderWidget(x_in, y_in, w_in, h_in, label_in, r_in, g_in, b_in, a_in, two_values, minval_in, maxval_in, default_in, unit_in, isInteger_in, hint_in )
  local()
  global(newUI)
  instance(x, y, w, h, r, g, b, a, default, minval, maxval, value, value2, label, lastleft, lastright, lastclick, yslidercenter, cap, twoval, unit, isInteger, thisUI, htime, hint, onmarker)
  (
      !thisUI ? thisUI = newUI+=1;
      x             = x_in+2;
      y             = y_in;
      w             = w_in-4;
      h             = h_in;
      r             = r_in;
      g             = g_in;
      b             = b_in;
      a             = a_in;
      yslidercenter = y + 0.3 * h;

      !label ? label = label_in;
      !hint ? hint  = hint_in;
      !unit ? unit  = unit_in;
      twoval        = two_values; /* Do we have two values? */
      minval        = minval_in;
      maxval        = maxval_in;
      default       = ( default_in - minval ) / ( maxval - minval );
      isInteger     = isInteger_in;
  );
  
function sliderWidget_setValue(value_in)
  local()
  global(gfx_x, gfx_y)
  instance(x, y, w, h, r, g, b, a, default, minval, maxval, value, value2, label, lastleft, lastclick, yslidercenter, cap, twoval)
  (
    value = ( value_in - minval ) / ( maxval - minval )
  );
  
function sliderWidget_getValue()
  local(v)
  global(gfx_x, gfx_y)
  instance(x, y, w, h, r, g, b, a, default, minval, maxval, value, value2, label, lastleft, lastclick, yslidercenter, cap, twoval, isInteger)
  (
    v = value * ( maxval - minval ) + minval;
    v = isInteger ? floor(v+0.499999) : v;
    v
  );  
  
function sliderWidget_draw()
  local(cscale, tmp, siz, q, str, modmax, modmin, modoffset)
  global(  slider38, gfx_x, gfx_y, lastUI, scaling, widgetFontSize, mlevel, SLIDER_FONT, 
      font_color_r, font_color_g, font_color_b, font_color_a,
      slider_marker_r, slider_marker_g, slider_marker_b, slider_marker_a,
      highlight_color_r, highlight_color_g, highlight_color_b, highlight_color_a,
      disabled_color_r, disabled_color_g, disabled_color_b, disabled_color_a, 
      modrange_r, modrange_g, modrange_b, modrange_a )
  instance(  x, y, w, h, 
      r, g, b, a, 
      default, minval, maxval, 
      value, value2, 
      label, unit, 
      overridevalue, overrideunit,
      lastleft, lastclick, yslidercenter, cap, twoval, thisUI, onmarker, 
      disabled, dynval, postMeter)
  (
  cscale = 1+scaling;
    gfx_setfont(SLIDER_FONT);
  
    ( disabled == 0 ) ? (
      gfx_rect( x-3*(1+scaling), yslidercenter-.25*h, w+6*cscale, h*.5 );
        
      lastUI == thisUI ? gfx_set( highlight_color_r, highlight_color_g, highlight_color_b, highlight_color_a ) : gfx_set( r, g, b, 0.8 );
      gfx_line(x, yslidercenter, x+w, yslidercenter);
      gfx_set( r*.3, g*.3, b*.3, 0.8 );
      gfx_line(x + cscale, yslidercenter + 1, x+w + cscale, yslidercenter + 1);

      gfx_set( r, g, b, .2 );    
      gfx_rect( x-2*cscale + value*w+5, yslidercenter - .3*h, 1, h*.6+1 );
      gfx_rect( x-2*cscale + value*w-2, yslidercenter - .3*h, 1, h*.6+1 );
      gfx_rect( x-2*cscale + value*w-1, yslidercenter - .3*h, 6, 1 );
      gfx_rect( x-2*cscale + value*w-1, yslidercenter + .3*h-1, 6, 1 );
      
      gfx_set( r, g, b, 1.0 );
      gfx_rect( x-2*cscale + value*w, yslidercenter - .2*h, 4*cscale, h*.4 );
      gfx_set( r*.2, g*.2, b*.2, 0.4 );
      gfx_rect( x-cscale + value*w, yslidercenter - .2*h + 1, 2*cscale, h*.4-2 );
      
      onmarker == 1 ? 
      (
        gfx_set( 1, 1, 1, 0.4 );
        gfx_rect( x-2*cscale + value*w-1, yslidercenter - .2*h-1, 4*cscale+2, h*.4+2 );
      );
      
      gfx_set( modrange_r, modrange_g, modrange_b, modrange_a );
      value2 > 0 ? gfx_rect( x + value*w, yslidercenter - .1*h, value2*w+1, h*.3 ) : gfx_rect( x + (value+value2)*w, yslidercenter - .1*h, abs(value2)*w, h*.3 );
  
      gfx_set( slider_marker_r, slider_marker_g, slider_marker_b, slider_marker_a );
    ) : (
      // Look of disabled widget
      gfx_set( disabled_color_r, disabled_color_g, disabled_color_b, disabled_color_a*.2 );
      gfx_rect( x-3*cscale, yslidercenter-.25*h, w+6*cscale, h*.5 );
      gfx_set( disabled_color_r, disabled_color_g, disabled_color_b, disabled_color_a );
      gfx_line(x + cscale, yslidercenter, x+w + cscale, yslidercenter);
    );
    
    disabled == 0 ? gfx_set( font_color_r, font_color_g, font_color_b, font_color_a );
    gfx_x = x;
    gfx_y = y + 11*cscale;
    gfx_printf( label );
    
    ( overridevalue == 0 ) ? (
      sprintf( str, "%2.3g%s", this.sliderWidget_getValue(), unit );
    ) : (
      ( overridevalue == -2000 ) ? (
        sprintf( str, "%s", overrideunit );
      ) : ( overridevalue == -1000 ) ? (
        sprintf( str, "%2.3g (%s)", this.sliderWidget_getValue(), overrideunit );
      ) : (
        ( overridevalue > 1000 ) ? (
          sprintf( str, "%2.3g (%2.3g k%s)", this.sliderWidget_getValue(), overridevalue/1000, overrideunit );
        ) : (
          sprintf( str, "%2.3g (%2.3g %s)", this.sliderWidget_getValue(), overridevalue, overrideunit );      
        )
      );
    );
    gfx_measurestr(str, siz, tmp);
    !postMeter ? (
      gfx_x = x + w - siz;
      gfx_y = y + 11*cscale;
    ) : (
      gfx_x = postMeter < 2 ? x + w + 4 : postMeter - siz - 2;
      gfx_y = y + 0.5*tmp;// + tmp;
    );
    gfx_printf( str );
  );
  
function sliderWidget_setValue2(value_in)
   local()
   global(gfx_x, gfx_y)
   instance(x, y, w, h, r, g, b, a, default, minval, maxval, value, value2, label, lastleft, lastclick, yslidercenter, cap, twoval)
   (
    value2 = value_in;
   );
   
// Set value in a non-normalized manner
function sliderWidget_setValue2_nn(value_in)
   local()
   global(gfx_x, gfx_y)
   instance(x, y, w, h, r, g, b, a, default, minval, maxval, value, value2, label, lastleft, lastclick, yslidercenter, cap, twoval)
   (
    value2 = ( value_in > 0 ) ? value_in * (1.0 - value) : value_in * value;
   );
  
function sliderWidget_getValue2()
   local()
   global(gfx_x, gfx_y)
   instance(x, y, w, h, r, g, b, a, default, minval, maxval, value, value2, label, lastleft, lastclick, yslidercenter, cap, twoval)
   (
    value2
   ); 
   
function sliderWidget_getValue2_nn()
   local()
   global(gfx_x, gfx_y)
   instance(x, y, w, h, r, g, b, a, default, minval, maxval, value, value2, label, lastleft, lastclick, yslidercenter, cap, twoval)
   (
    value2 > 0 ? value2 / (1.0 - value) : value2 / value
   );    
  
function sliderWidget_processMouse(xx, yy, dx, dy, mc)
  local(left, right, change, newcap, onslider, shft, ctrl, mul, ym)
  global(gfx_x, gfx_y, mouse_wheel, scaling, mouse_cap, lastUI, lastChar, hinter.updateHintTime)
  instance(x, y, w, h, r, g, b, a, default, minval, maxval, value, value2, label, lastleft, lastright, lastclick, yslidercenter, cap, twoval, thisUI, hint, onmarker, lastrightclick, disabled)
  (
    disabled == 0 ? 
    (
      left  = mc & 1;
      right = mc & 2;
      ctrl  = mc & 4;
      shft  = mc & 8;
      
      change = 0;
      newcap = 0;
      ym = .5*h;
      onslider = ( ( xx > x ) && ( xx < (x+w) ) && ( yy > (yslidercenter-ym) ) && ( yy < (yslidercenter+ym) ) );
      onmarker = abs( value * w + x - xx ) < 8*(1+scaling) && ( yy > (yslidercenter-ym) ) && ( yy < (yslidercenter+ym) );
      
      onslider ? (
        hinter.updateHintTime(hint);
      );
      
      ( lastUI == thisUI ) ?
      (
        ( lastChar == 1919379572 ) ? value = value + .001;
        ( lastChar == 1818584692 ) ? value = value - .001;      
      );
      
      left ? (
        ( ( lastleft == 0 ) && ( time_precise() - lastclick ) < .2 ) ?
        (
          value       = default;
          change      = 1;
        ) : ( ( cap == 1 ) || ( ( onslider || onmarker ) && lastleft == 0 ) ) ? ( 
          ((shft == 0) && (ctrl == 0)) ? (
            value       = (xx-x)/w;
          ) : (
            mul         = 1;
            shft ? mul *= 0.125;
            ctrl ? mul *= 0.1666666666667;
            value       = value - mul*dx/w;
          );
          change      = 1;
          newcap      = 1;
          lastclick   = time_precise();
          lastUI      = thisUI;
        );
      ) : ( 
        twoval && right ? 
        (
          ( ( cap == 2 ) || ((onmarker || onslider) && lastright == 0) ) ? ( 
            ( ( lastright == 0 ) && ( time_precise() - lastrightclick ) < .15 ) ?
            (
              value2 = 0;
              change = 2;
              lastrightclick = time_precise();
            ) : (
              value2 = (xx-x)/w - value;
              change = 2;
              newcap = 2;
              lastrightclick = time_precise();
            )
          );
        ) : ( (mouse_wheel ~= 0) && onslider ) ? (
          ( shft && twoval ) ? (
            value2 = value2 + .00001 * mouse_wheel; change = 1;
          ) : (
            value = value + .00001 * mouse_wheel;  change = 1;
          );
          mouse_wheel = 0;
        );
      );
      
      lastleft  = left;
      lastright = right;
      cap       = newcap;
      value     = value > 1.0 ? 1 : value;
      value     = value < 0.0 ? 0 : value;
      twoval ? (
        value2 = (value2+value) > 1.0 ? 1.0-value : value2;
        value2 = (value2+value) < 0.0 ? -value : value2;    
      );
      
      change
    ) : 0;
  );  
  
/*------------------*/
/* Selection button */
/*------------------*/
function selectionButton(x_in, y_in, w_in, h_in, label_in, r_in, g_in, b_in, a_in, hint_in)
  local()
  global()
  instance(x, y, w, h, r, g, b, a, active, label, lastleft, hint)
  (
      x       = x_in;
      y       = y_in;
      w       = w_in;
      h       = h_in;
      r       = r_in;
      g       = g_in;
      b       = b_in;
      a       = a_in;
      !label  ? label = label_in;
      !hint   ? hint = hint_in;
  );  
  
function selectionButton_draw()
  local(jnk, siz, hln)
  global(gfx_x, gfx_y, buttonFontSize, BUTTON_FONT,
         font_color_r, font_color_g, font_color_b, font_color_a,
         bg_color_r, bg_color_g, bg_color_b )
  instance(x, y, w, h, r, g, b, a, active, label, lastleft, htime, style, mode)
  (
    style == 2 ? (
      hln = (.17 + .3*(1-active))*htime;
      gfx_set( bg_color_r, bg_color_g, bg_color_b, 1 );
      gfx_rect( x, y, ceil(w), ceil(h) );
    );
    
    hln = (.17 + .3*(1-active))*htime;
    gfx_set( min(1,r + hln), min(1,g + hln), min(1,b + hln), .1 + .5*active );
    ( mode == 0 ) ? (
      gfx_rect( x, y, ceil(w), ceil(h) );
    ) : ( mode == 1 ) ? (
      gfx_rect( x, y, ceil(w*.5), ceil(h) );
    ) : (
      gfx_rect( ceil(x+.5*w), y, ceil(w*.5), ceil(h) );
    );
  gfx_set( min(1,r + hln), min(1,g + hln), min(1,b + hln), .1 + .5*active );
    
    gfx_set( r, g, b, a );
    gfx_line(x, y, x, y+h);
    gfx_line(x+1, y, x+w-1, y);
    gfx_line(x+w, y, x+w, y+h);
    gfx_line(x+1, y+h, x+w-1, y+h);
    gfx_set( font_color_r, font_color_g, font_color_b, font_color_a );
    gfx_setfont(BUTTON_FONT);
    gfx_measurestr(label, siz, jnk);
    gfx_x = x + .5 * (w-siz);
    gfx_y = y - .5*(jnk-h) + 1;
    gfx_printf( label );
  );
  
function selectionButton_processMouse(xx, yy, mc)
  local(left, right, change)
  global(hinter.updateHintTime, gfx_x, gfx_y, delta_time)
  instance(x, y, w, h, r, g, b, a, active, label, align, lastright, lastleft, htime, hint, modes, mode, hoverType)
  (
    change = 0;
    left = mc & 1;
    right = mc & 2;
    ( xx > x ) && ( xx < (x+w) ) && ( yy > y ) && ( yy < (y+h) ) ?
    (
      left ? (
        ( (lastleft ~= 1 ) ) ? ( 
          active = 1 - active;
          change = 1;
        );
      );
      
      right ? (
        ( (lastright ~= 2 ) ) ? ( 
          change = 2;
          
          mode = mode + 1;
          mode > modes ? mode = 0;
        );        
      );
      
      hinter.updateHintTime(hint);
    );
    
    lastleft = left;
    lastright = right;
    change
  );
  
/*----------------*/
/* Slider         */
/*----------------*/
  function drawSimpleSlider(_x, _y, _w, _h, r, g, b, a, _hint)
  local()
  instance(thisUI, value, x, y, w, h, over, cap, hint)
  global(lastUI)
  (
    !hint ? hint = _hint;
    
    x = _x; y = _y; w = _w; h = _h;
    gfx_set( .12, .12, .2, .1 );  
    gfx_rect(x, y, w, h);
  
    gfx_set( r, g, b, a );
    gfx_line(x, y, x, y+h);
    gfx_line(x+1, y, x+w-1, y);
    gfx_line(x+w, y, x+w, y+h);
    gfx_line(x+1, y+h, x+w-1, y+h);

    gfx_line(x+value*w, y, x+value*w, y+h);
    gfx_set( r, g, b, .3*a );
    gfx_line(x+value*w+2, y, x+value*w+2, y+h);
    gfx_line(x+value*w-2, y, x+value*w-2, y+h);
    
    (over || cap) ? (
      gfx_set( r*2, g*2, b*2, a );
      gfx_line(x+value*w, y, x+value*w, y+h);
      gfx_set( r, g, b, .5*a );
      gfx_line(x+value*w+2, y, x+value*w+2, y+h);
      gfx_line(x+value*w-2, y, x+value*w-2, y+h);
      gfx_line(x+value*w+3, y, x+value*w+3, y+h);
      gfx_line(x+value*w-3, y, x+value*w-3, y+h);
    );
  );
  
  function simpleSlider_processMouse(mx, my, mc, default)
  local(xmi, xma, mul, left, lleft, doubleClick, cTime, change)
  instance(x, y, w, h, over, hint, lastLeftClick, cap, value, lx)
  global(hinter.updateHintTime)
  (
    xmi = (x+value*w-5);
    xma = (x+value*w+5);
    
    mul = 1;
    over = ( mx > xmi ) && ( mx < xma ) && ( my > y ) && ( my < (y+h) );
    (mc&4) ? mul = mul * 0.1666666666667; /* CTRL */
    (mc&8) ? mul = mul * 0.125; /* SHIFT */
    
    left = mc & 1;
    
    /*( over == 1 ) ? (
      (mouse_wheel ~= 0) ? (
        value = value + 0.0001 * mul * mouse_wheel;
        mouse_wheel = 0;
        value = clamp(value, 0, 1);
        change = 1;
      );
    );*/
    
    ( left == 0 ) ? (
      ( over == 1 ) ? (
        hinter.updateHintTime(hint);
      );
    );
    
    doubleClick = 0;
    (left && !lleft) ? (
       time_precise(cTime);
       ( ( cTime - lastLeftClick ) < .25 ) ? (
          doubleClick = 1;
       ) : lastLeftClick = cTime;
    );
    
    ( left && cap == 1 ) ? (
      value = value + .01*mul*(mx - lx);
      value = clamp(value, 0, 1);
      change = 1;
    ) : ( cap = 0; );
    
    ( left && !lleft ) ? 
    (
      ( over ) ?
      (
        doubleClick ? (
          lastLeftClick = -100;
          change = 1;
          value = default;
        ) : ( 
          cap = 1;
        );
      );
    );
    
    lleft = left;
    lx = mx;
    
    change
  );  
  
function roundRect(x, y, w, h, radius, aa)
local()
instance()
global(gfx_a)
(
  x = floor(x);
  y = floor(y);
  w = floor(w);
  h = floor(h);
  
  gfx_circle(x+radius, y+radius, radius, 1, aa);
  gfx_circle(x+w-radius-1, y+radius, radius, 1, aa);
  gfx_circle(x+w-radius-1, y+h-radius-1, radius, 1, aa);
  gfx_circle(x+radius, y+h-radius-1, radius, 1, aa);
  
  gfx_rect(x, y+radius, radius*2, h-radius*2, aa);
  gfx_rect(x+w-radius*2, y+radius, radius*2, h-radius*2, aa);
  gfx_rect(x+radius, y, w-radius*2, radius*2, aa);
  gfx_rect(x+radius, y+h-radius*2, w-radius*2, aa*2, aa);
  gfx_rect(x+radius, y+radius, w-radius*2, h-radius, aa);
);

function drawSelectionButton(str, x, y, w, h, idx, slider_idx)
local(ww, hh)
global(knob_font_color_r, knob_font_color_g, knob_font_color_b, knob_font_color_a, gfx_x, gfx_y,
       mouse_cap, mouse_x, mouse_y, lcap)
instance()
(
  x = floor(x);
  y = floor(y);
  w = floor(w);
  h = floor(h);
  
  (lcap == 0) && (mouse_cap & 1 == 1) ? (
    mouse_x > x && mouse_x < (x+w) && mouse_y > y && mouse_y < (y+h) ? (
      slider(slider_idx) = idx;
    );
  );
  
  idx == slider(slider_idx) ? (
    gfx_set(knob_font_color_r, knob_font_color_g, knob_font_color_b, knob_font_color_a);
    gfx_roundrect(x, y, w, h, 2);
  ) : (
    gfx_set(.1, .13, .1, 1);
    roundrect(x, y, w, h, 2, 1);
    gfx_set(.2, .23, .2, 1);
    gfx_roundrect(x, y, w, h, 2);
  );
  
  gfx_set(knob_font_color_r, knob_font_color_g, knob_font_color_b, knob_font_color_a);
  gfx_measurestr(str, ww, hh);
  gfx_x = x + .5 * (w - ww);
  gfx_y = y + .5 * (h - hh);
  gfx_printf(str);
);

function drawPanel(str, x, y, w, h, active)
local(fact, sx, sy, label_height)
global(BAND_FONT, edge_r, edge_g, edge_b, edge_a, edge_font_r, edge_font_g, edge_font_b, scaling, style, 
       font_color_r, font_color_g, font_color_b, gfx_x, gfx_y)
(
  style == 0 ? (
    gfx_set(0, 0, 0, .4);
    roundRect(x+5, y+5, w, h, 12, 1);
  
    gfx_set(edge_r, edge_g, edge_b, edge_a);
    drawVertLabel(x, y, str, h, BAND_FONT, 3, edge_font_r, edge_font_g, edge_font_b, 1.0, 1.0);
    
    fact = .5 + .2 * active;
    gfx_set(fact * edge_r, fact * edge_g, fact * edge_b, 1.0);
    roundRect(x, y, w, h, 12, 1);
    fact = .6 + .4 * active;
    gfx_set(fact * edge_r, fact * edge_g,  fact * edge_b, 1.0);
    roundRect(x+3, y+3, w-6, h-6, 8, 1);
    
    x + w
  ) : ( style == 1 ) ? (
    y -= .19 * h;
    h = 1.18 * h;
    label_height = .2;
    fact = .5 + .2 * active;
    gfx_set(fact * edge_r, fact * edge_g, fact * edge_b, 1.0);
    roundRect(x, y, w, h, 3, 1);
    //fact = .6 + .4 * active;
    //gfx_set(fact * edge_r, fact * edge_g,  fact * edge_b, 1.0);
    //roundRect(x+3, y+3, w-6, h-6, 2, 1);

    gfx_set(65/255, 65/255, 66/255, 1.0);
    roundRect(x, y, w, h*label_height, 3, 1);

    gfx_set(font_color_r, font_color_g, font_color_b, 1.0);
    gfx_setfont(BAND_FONT);
    gfx_measurestr(str, sx, sy);
    gfx_x = x + .5 * (w - sx);
    gfx_y = y + .5 * (label_height*h - sy);
    gfx_printf(str);
    
    x + w
  ) : (
    y -= .19 * h;
    h = 1.18 * h;
    label_height = .2;
    fact = .5 + .2 * active;
    gfx_set(0.4 * active, .2 + .6 * active, 0.4 * active, 1.0);
    roundRect(x, y, w, h, 3, 1);
    gfx_set(0.01, 0.03, 0.01, 1.0);
    roundRect(x+1, y+1, w-2, h-2, 3, 1);    
    //fact = .6 + .4 * active;
    //gfx_set(fact * edge_r, fact * edge_g,  fact * edge_b, 1.0);
    //roundRect(x+3, y+3, w-6, h-6, 2, 1);

    gfx_set(0.4 * active, .2 + .6 * active, 0.4 * active, 1.0);
    roundRect(x, y, w, h*label_height, 3, 1);
    gfx_set(0.04, 0.09, 0.04, 1.0);
    roundRect(x+1, y+1, w-2, h*label_height-2, 3, 1);

    gfx_set(font_color_r, font_color_g, font_color_b, .2 + .8 * active);
    gfx_setfont(BAND_FONT);
    gfx_measurestr(str, sx, sy);
    gfx_x = x + .5 * (w - sx);
    gfx_y = y + .5 * (label_height*h - sy);
    gfx_printf(str);
    
    x + w
  );;
);
  
/*-----------*/
/* KNOB      */  
/*-----------*/   
/* Knob handling */
function drawKnobLabels(radius, value, label)
(
  gfx_set(knob_font_color_r, knob_font_color_g, knob_font_color_b, knob_font_color_a);
  gfx_setfont(3, "Arial", 10*(1+scaling));
  ang = .75*$pi + 1.5*$pi*value;
  gfx_measurestr(label, ww, hh);
  gfx_x = floor(floor(cX + radius * cos(ang)) - .5*ww) + 1;
  gfx_y = floor(floor(cY + radius * sin(ang)) - .25*hh);
  gfx_printf(label);
);

function drawKnob(_x, _y, _r, text, _hint, offset)
  local(ang, tang, dang, r0, r1, r2, r3, rk, tw, th, hc, cy,
        rc, gc, bc)
  instance(x, y, r, value, value2, value3, active, label, hint, hovertime, bipolarValue2, bipolarValue3, y_font_offset, xr, yr)
  global(gfx_a, gfx_x, gfx_y, scaling, KNOB_FONT, KNOB_FONT2,
         mod1_color_r, mod1_color_g, mod1_color_b, mod1_color_a,
         mod2_color_r, mod2_color_g, mod2_color_b, mod2_color_a,
         knob_font_color_r, knob_font_color_g, knob_font_color_b, knob_font_color_a,
         render_time)
  (
    x = _x;
    y = _y;
    r = _r;
    
    r0 = .58*r;
    r1 = 1.2*r;
    r2 = 1.4*r;
    r3 = 1.7*r;
    rk = .1*r;
    
    hc = min(hovertime, 1);
    
    active ? (
      rc = 37/255;
      gc = 56/255;
      bc = 217/255;
    ) : (
      rc = 111/255;
      gc = 111/255;
      bc = 111/255;
    );
    
    cy = .3 + .2*sin(offset + 4*render_time);
    gfx_set(rc, gc, bc, 0.9*cy);
    gfx_circle(x, y, r+1, 1, 1);
    gfx_circle(x, y, r, 1, 1);
    gfx_set(.1+.2*cy, .5+.9*cy, .1+.3*cy, .5);
    
    gfx_set(rc, gc, bc, cy);
    gfx_circle(x, y, r-3, 1, 1);
    gfx_set(0, 0, 0, .9);
    gfx_circle(x, y, .93*r, 1, 1);

    gfx_set(.3, .25, .36, .2-.1*cy);
    gfx_circle(x, y, .8*r, 1, 1);
    gfx_set(.1, .1, .1, .3*cy);
    gfx_circle(x, y, .7*r, 1, 1);
    hint = _hint;

    !active ? (
      gfx_set(0, 0, 0, .5);
      gfx_circle(x, y, r, 1, 1);
    );    
  
    gfx_set(0, 0, 0, .2);
    gfx_circle(x, y, .78*r, 0, 1);
    gfx_circle(x, y, .76*r, 0, 1);
    gfx_set(.3, .4, .8, 1);
    
    // 31 segments
    /*ang = .75*$pi;
    tang = 1.5*$pi;
    dang = tang/30;
    loop(6,
      gfx_line(x + r1 * cos(ang), y + r1 * sin(ang), x + r3 * cos(ang), y + r3 * sin(ang) );
      ang += dang;
      loop(4,
        gfx_line(x + r1 * cos(ang), y + r1 * sin(ang), x + r2 * cos(ang), y + r2 * sin(ang) );
        ang += dang;
      );
    );
    gfx_line(x + r1 * cos(ang), y + r1 * sin(ang), x + r3 * cos(ang), y + r3 * sin(ang) );*/
    
    ang = .75*$pi + 1.5*$pi*value;
    active ? (
      gfx_set(0, 0, 0, .9);
      xr = floor(x + r0 * cos(ang));
      yr = floor(y + r0 * sin(ang));
      gfx_circle( xr, yr, .9*rk, 1, 1 );
      gfx_set(.2+.2*cy, .2+.4*cy, .6+.3*cy, 1);
      gfx_circle( xr, yr, .7*rk, 1, 1 );
    );
    
    gfx_set(.4, .6, .9, 1);    
    gfx_setfont(KNOB_FONT);
    gfx_measurestr(text, tw, th);
    gfx_x = x - .5*tw;
    gfx_y = y + r + .25*th - y_font_offset;
    active ? (
      gfx_set(knob_font_color_r, knob_font_color_g, knob_font_color_b, knob_font_color_a);
    ) : (
      gfx_set(knob_font_color_r, knob_font_color_g, knob_font_color_b, .3*knob_font_color_a);    
    );
    gfx_printf(text);
    
    gfx_setfont(KNOB_FONT2);    
    label && active ? (
      gfx_measurestr(label, tw, th);

      /*gfx_set(0, 0, 0, .5);
      gfx_x = x - floor(.5*tw) - 1;
      gfx_y = y - .5*th + 1;
      gfx_printf(label);
      gfx_x = x - floor(.5*tw) + 1;
      gfx_printf(label);*/
    
      gfx_set(knob_font_color_r, knob_font_color_g, knob_font_color_b, knob_font_color_a);
      gfx_x = x - floor(.5*tw);
      gfx_y = y - .5*th + 1;
      gfx_printf(label);
      
      gfx_a = .25;
      gfx_x = x - floor(.5*tw) + 1;
      gfx_y += 1;
      gfx_printf(label);
    );
  );
  
function knob_set(in_label, in_active, in_value)
  instance(label, active, value)
  global()
  (
    label = in_label;
    active = in_active;
    value = in_value;
  );

function knob_over(mx, my)
  instance(x, y, r)
  global()
  local(dx, dy)
  (
    dx = (mx-x);
    dy = (my-y);
    (dx*dx + dy*dy) < (r*r)
  );

function _knob_processMouse(mx, my, mousecap, default)
  local(left, dx, dy, change, mul, over)
  instance(hint, value, x, y, r, cap, lleft, lx, ly, active, lastLeftClick, doubleClick, cTime, hoverTime)
  global(hinter.updateHintTime, mouse_wheel, delta_time, comboboxOpen, activeModifier)
  (
    change = 0;
    !comboboxOpen ? (
      mul = 1;
      
      dx = (mx-x);
      dy = (my-y);
      over = (dx*dx + dy*dy) < (r*r);
  
      (mousecap&4) ? mul = mul * 0.1666666666667; /* CTRL */
      (mousecap&8) ? mul = mul * 0.125; /* SHIFT */
      
      (over || (cap > 0)) ? (
        hoverTime = hoverTime + delta_time;
      ) : ( 
        hoverTime = 0;
      );
      
      active ? (
        left = mousecap & 1;
        
        ( over == 1 ) ? (
          (mouse_wheel ~= 0) ? (
            value = value + 0.0001 * mul * mouse_wheel;
            mouse_wheel = 0;
            value = clamp(value, 0, 1);
            change = 1;
          );
        );
        
        ( left == 0 ) ? (
          ( over == 1 ) ? (
            hinter.updateHintTime(hint);
          ) : ( 
            hinter.updateHintTime(0);
          );
        );
        
        doubleClick = 0;
        (left && !lleft) ? (
           time_precise(cTime);
           ( ( cTime - lastLeftClick ) < .25 ) ? (
              doubleClick = 1;
           ) : lastLeftClick = cTime;
        );
        
        ( left && cap == 1 ) ? (
          value = value - .01*mul*(my - ly);
          change = 1;
        ) : ( cap = 0; );
        
        ( left && !lleft ) ? 
        (
          ( over ) ?
          (
            doubleClick ? (
              lastLeftClick = -100;
              change = 1;
              value = default;
            ) : ( 
              cap = 1;
            );
          );
        );
        
        lleft = left;
        lx = mx;
        ly = my;
      );
    );
    
    change
  );
  
function knob_processMouse(mx, my, mousecap, default)  
  local(ret)
  instance(value)
  global()
  (
    ret = this._knob_processMouse(mx, my, mousecap, default);
    value = clamp(value, 0, 1);
    ret
  );

function knob_draw_modifier(value2, bipolar, red, g, b, a, modifier)
  local(r_base, ang, vstart, vend, tang, mx, my, mc, cy, vs, ve)
  instance(x, y, r, modifier_setpoint, value)
  global(gfx_a, mouse_x, mouse_y, mouse_cap, activeModifier, render_time)
  (
    ang = 1.25*$pi;
    tang = 1.5*$pi;
    
    cy = .3 + .2*sin(14*render_time);    
    
    gfx_set(red, g, b, .55*cy);
    modifier == activeModifier ? (
      gfx_circle(x, y, .8*r, 1, 1);
    );
    
    ( value2 != 0 ) ? (
      gfx_set(red, g, b, a);
      bipolar ? (
        vstart  = clamp(value - .5*value2, 0, 1);
        vend    = clamp(value + .5*value2, 0, 1);
      ) : (
        vstart  = clamp(value, 0, 1);
        vend    = clamp(value + value2, 0, 1);
      );
      
      r_base = floor(r - 4*modifier-1);
//      r_base = floor(r + 4*modifier-1);
      ( vstart < vend ) ? (
        gfx_a = 1 * gfx_a;
        vs = ang + tang * vstart;
        ve = ang + tang * vend;
        gfx_arc(x, y, r_base+2, vs, ve, 1);
        gfx_arc(x, y, r_base+1, vs, ve, 1);
        gfx_arc(x, y, r_base, vs, ve, 1);
        gfx_a = 2 * gfx_a;
        gfx_arc(x, y, r_base+1, vs, ve, 1);
      ) : (
        gfx_a = 1 * gfx_a;
        vs = ang + tang * vend;
        ve = ang + tang * vstart;
        gfx_arc(x, y, r_base+2, vs, ve, 1);
        gfx_arc(x, y, r_base+1, vs, ve, 1);
        gfx_arc(x, y, r_base, vs, ve, 1);
        gfx_a = 2 * gfx_a;
        gfx_arc(x, y, r_base+1, vs, ve, 1);
      );
    );
  );
  
  function knob_modifier_processMouse(default, value2)
  instance(value)
  global(mouse_x, mouse_y, mouse_cap)
  local(ret)
  (
    // This is so horrible :-D
    value = value2;
    ret = this._knob_processMouse(mouse_x, mouse_y, mouse_cap, default);
    value = max(-1, min(1, value));
    
    ret
  );


/*-----------*/
/* BUTTON    */  
/*-----------*/   
  function drawButton( modButtonX, modButtonY, modButtonW, modButtonH, str, font, taper )
  instance(x, y, w, h, over)
  global(backface_color_r, backface_color_g, backface_color_b,
         font_color_r, font_color_g, font_color_b, font_color_a,
         gfx_x, gfx_y)
  local(strw, strh)
  (
    gfx_set(backface_color_r, backface_color_g, backface_color_b, 0.9);
    
    gfx_triangle(modButtonX+taper,              modButtonY, 
                 modButtonX+modButtonW-taper,   modButtonY, 
                 modButtonX+modButtonW,         modButtonY + modButtonH,
                 modButtonX+modButtonW,         modButtonY + modButtonH,
                 modButtonX,                    modButtonY + modButtonH
                  );
    
    gfx_set(font_color_r, font_color_g, font_color_b, font_color_a);
    gfx_setfont(font);
    gfx_measurestr(str, strw, strh);
    gfx_x = modButtonX + .5*(modButtonW-strw);
    gfx_y = modButtonY + .5*(modButtonH-strh);
    gfx_printf(str);
    
    over ? (
      gfx_set(font_color_r, font_color_g, font_color_b, .5*font_color_a);
      gfx_x = modButtonX + .5*(modButtonW-strw) + 1;
      gfx_printf(str);
    );
    
    x = modButtonX;
    y = modButtonY;
    w = modButtonW;
    h = modButtonH;
  );
  
  function button_processMouse(mx, my, mc)
  instance(x, y, w, h, over, lmc)
  global()
  local(clicked)
  (
    over = ( mx > x ) && ( mx < (x+w) ) && ( my > y ) && ( my < ( y + h ) );
    
    over ? (
      clicked = ((mc & 1) && (lmc & 1 == 0)) ? 1 : 0;
    ) : clicked = 0;
    
    lmc = mc;
    
    clicked
  );
  
/*-----------*/
/* TOGGLE    */  
/*-----------*/ 
function processMouseToggle(mx, my, mousecap)
  instance(xHit, yHit, wHit, hHit, on, lastleft, str)
  local(left, slack, over)
  global(hinter.updateHintTime, comboboxOpen)
  (
    !comboboxOpen ? (
      slack = 2;
      left = mousecap & 1;
      
      over = ( (mx >= (xHit-slack)) && ( mx <= (xHit+wHit+slack) ) && ( my >= (yHit-slack) ) && ( my <= (yHit+hHit+slack) ) );
      
      over ? (
        ( (left == 1) && (lastleft == 0) ) ?  (
          on = 1 - on;
        );
        hinter.updateHintTime(str);
      );
      
      lastleft = left;
    );
    
    on
  );
 
function drawToggle(_x, _y, _w, _h, _on, wr, wg, wb, wa, r, g, b, a, _str)
  local(ww, hh, r)
  instance(x, y, w, h, str, on, invert, label, xHit, yHit, wHit, hHit, inactive)
  global(gfx_x, gfx_y, gfx_a, gfx_mode, 
         TOGGLE_FONT, knob_font_color_r, knob_font_color_g, knob_font_color_b, knob_font_color_a)
  (
    x = _x;
    y = _y;
    w = _w;
    h = _h;
    on = _on;
    str = _str;
    
    x = floor(x);
    y = floor(y);
    w = floor(w);
    h = floor(h);
    
    xHit = x;
    yHit = y;
    wHit = w;
    hHit = h;

    gfx_set(0, 0, 0, 0);
    gfx_rect(x, y, w, h);
    
    inactive ? (
      b = g = r;
      wg = wb = wr;
    );
    
    gfx_set(r, g, b, a*.2);
    gfx_rect(x, y, w, h);
    
    gfx_set(wr, wg, wb, wa);
    gfx_line(x, y, x+w, y);
    gfx_line(x, y, x, y+h);
    gfx_line(x+w, y, x+w, y+h);
    gfx_line(x, y+h, x+w, y+h);

    ( label ) ? (
      gfx_set(knob_font_color_r, knob_font_color_g, knob_font_color_b, knob_font_color_a);
      gfx_setfont(TOGGLE_FONT);
      gfx_measurestr(label, ww, hh);
      gfx_x = floor(x+1.5*w);
      gfx_y = floor(y-.5*hh+.5*h);
      gfx_printf(label);
      
      xHit = x;
      yHit = y;
      wHit = w + 1.5 * w + ww;
      hHit = hh;
    );
    
    !inactive ? (
      ( (on && !invert) || (!on && invert) ) ? (
        gfx_set(r, g, b, a);
        gfx_rect(x, y, w, h);
        gfx_a *= .36;
        gfx_rect(x, y, w, h);
        gfx_a *= .6;
        gfx_rect(x+1, y+1, w-2, h-2);
        r = r*w*.4;
        g = g*w*.4;
        b = b*w*.4;
        gfx_set(r, g, b, gfx_a);
        loop(10,
          gfx_a *= .7;
          r *= 1.3;
          gfx_circle(floor(x+.5*w), floor(y+.5*h), 2*r, 2*r);
        );
      );
    );
  );

/*-----------*/
/* CIRCLE    */  
/*-----------*/
function fancyCircle(x1, y1, rin, mute, solo, bypass)
  local(dx)
  global(globalTime, lineR, lineG, lineB, lineA, lineHighlightR, lineHighlightG, lineHighlightB)
  instance(hl, x, y, r, over, gfx_mode)
  (
    x = x1;
    y = y1;
    rin == 0 ? r = 10 : r = rin;
    
    over ? (
      hl = abs(sin(2*globalTime));
      gfx_set(lineHighlightR, lineHighlightG, lineHighlightB, .1*hl);
      gfx_circle(x, y, 1.2*r, 1, 1);
      gfx_set(lineHighlightR, lineHighlightG, lineHighlightB, .1*hl);
      gfx_circle(x, y, 1.5*r, 1, 1);
      gfx_set(lineHighlightR, lineHighlightG, lineHighlightB, .05*hl);
      gfx_circle(x, y, 1.9*r, 1, 1);
      gfx_set(lineHighlightR, lineHighlightG, lineHighlightB, .03*hl);
      gfx_circle(x, y, 4*r, 1, 1);
    ); 
    
    gfx_set(.2*lineR, .2*lineG, .2*lineB, 1);
    gfx_circle(x, y, r, 1, 1);
    
    gfx_set(lineR, lineG, lineB, lineA);    
    gfx_circle(x, y, .92*r, 1, 1);
    
    gfx_set(.8*lineR, .8*lineG, .8*lineB, lineA);
    gfx_circle(x, y, .8*r, 1, 1);
    
    gfx_set(.6*lineR, .6*lineG, .6*lineB, lineA);
    gfx_circle(x, y, .75*r, 1, 1);
    
    gfx_set(.4*lineR, .4*lineG, .4*lineB, lineA);
    gfx_circle(x, y, .7*r, 1, 1);
    
    ( solo == 1 ) ? (
      gfx_set(.2, 1, .2, .5);
      gfx_circle(x, y, .8*r, 1, 1);
      gfx_set(.2, 1, .2, .5);
      gfx_circle(x, y, .9*r, 1, 1);
      gfx_set(.2, 1, .2, .5);
      gfx_circle(x, y, r, 1, 1);
      gfx_set(.2, 1, .2, .2);
      gfx_circle(x, y, 1.2*r, 1, 1);    
      
      gfx_set(.7, 1, .7, .3);
      gfx_circle(x, y, .7*r, 1, 1);
      
      gfx_set(.2, 1, .5, .08);
      gfx_circle(x, y, 1.1*r, 1, 0);
      gfx_set(.2, 1, .5, .08);
      gfx_circle(x, y, 1.2*r, 1, 0);
      gfx_set(.2, 1, .5, .08);
      gfx_circle(x, y, 1.4*r, 1, 0);
     ) : ( mute == 1 ) ? (
      gfx_set(1, .2, .2, .5);
      gfx_circle(x, y, .8*r, 1, 1);
      gfx_set(1, .2, .2, .5);
      gfx_circle(x, y, .9*r, 1, 1);
      gfx_set(1, .2, .2, .5);
      gfx_circle(x, y, r, 1, 1);
      gfx_set(1, .2, .2, .2);
      gfx_circle(x, y, 1.2*r, 1, 1);
      
      gfx_set(1, .55, .55, 1);
      gfx_circle(x, y, 1*r, 0, 1);
      
      gfx_set(1, .7, .7, .1);
      gfx_circle(x, y, .7*r, 1, 1);
      
      gfx_set(1, .5, .5, .1);
      gfx_circle(x, y, 1.1*r, 1, 0);
      gfx_set(1, .5, .5, .1);
      gfx_circle(x, y, 1.2*r, 1, 0);
      gfx_set(1, .5, .5, .1);
      gfx_circle(x, y, 1.4*r, 1, 0);
    );    
    
    ( bypass == 1 ) ? (
      gfx_set(1-mute, .2, .2, 1);
      dx = .5*r;
      gfx_line(x-dx, y-dx-1, x+dx, y+dx-1);
      gfx_line(x-dx, y-dx+1, x+dx, y+dx+1);
      gfx_line(x+dx, y-dx-1, x-dx, y+dx-1);
      gfx_line(x+dx, y-dx+1, x-dx, y+dx+1);    
      gfx_line(x+dx, y-dx,   x-dx, y+dx);
      gfx_line(x-dx, y-dx,   x+dx, y+dx);
    );
    
    over ? ( 
      gfx_set(1,1,1,.4*hl);
      gfx_circle(x, y, .6*r, 1, 1);
      gfx_circle(x, y, 1.4*r, 0, 1);
      gfx_circle(x, y, 1.5*r, 0, 1);
    );
    
    over = 0;
  );
