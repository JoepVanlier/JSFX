/* Graintable library */

@init
/* Cosine rise. N is number of samples rise time. */
function grainPrepRise(N)
local(w, ip)
instance(y0, y1, y2, b1)
global()  
(
  w = $pi/(N-1);
  ip = -.5*$pi;
  
  y0 = 0;
  b1 = 2.0 * cos(w);
  y1 = sin( ip - w );
  y2 = sin( ip - 2.0*w );
);
  
/* Cosine fall. N is number of samples fall time. */
function grainPrepFall(N)
local(w, ip)
instance(y0, y1, y2, b1)
global()  
(
  w = $pi/(N-1);
  ip = .5*$pi;
  
  y0 = 0;
  b1 = 2.0 * cos(w);
  y1 = sin( ip - w );
  y2 = sin( ip - 2.0*w );
);

function initGrain(_ptr, _nTransition, nHold, _ptrMin, _ptrMax, speed, _lmul, _rmul)
local()
instance(advance, loc, ptrMin, ptrMax, nTransition, N2, N3, idx, ds, lmul, rmul)
global()  
(
  ( idx <= N3 ) ? ( 0 ) : (
    loc = .5*_ptr;
    ptrMin = _ptrMin;
    ptrMax = _ptrMax;
    lmul = _lmul;
    rmul = _rmul;
    nTransition = _nTransition;
    N2 = nTransition + nHold;
    N3 = N2 + nHold;
    advance = speed;
    idx = 0;
    loc < .5*ptrMin ? (
      loc += .5*(ptrMax - ptrMin);
    );
    this.grainPrepRise(nTransition);
    1
  );
);

function fadeIn(_N)
local()
global()
instance(N, idx)
(
  N = _N;
  idx = 0;
  this.grainPrepRise(N);
);
  
function evaluateFade()
local()
instance(y0, y1, y2, b1, N, idx)
global()
(
  ( idx < N ) ?
  (
    idx += 1;
    y0 = b1 * y1 - y2;
    y2 = y1;
    y1 = y0;
    .5*(y0+1);
  ) : (
    1
  )
);

function grainRead()
local(out, frac,
      id0, id1, id2, id3,
      ism1, is0, is1, is2)
instance(ptr, 
         nTransition, N2, N3, idx,
         y0, y1, y2, b1, lOut, rOut, outL, outR,
         loc, hloc, ptrMin, ptrMax, 
         advance, hadvance, lmul, rmul)
global()
(
  ( idx < N3 ) ? (
    ptr = 2*loc - floor(2*loc) + floor(loc)*2;
    frac = loc - floor(loc);
    ptr = floor(ptr);
  
    /* Evaluate the cosine grain windowing function */
    idx < nTransition ? (
      // Continue the window
      y0 = b1 * y1 - y2;
      y2 = y1;
      y1 = y0;
      out = .5*(y0+1);
    ) : ( idx < N2 ) ? (
      out = 1;
    ) : (
      ( idx == N2 ) ? (
        this.grainPrepFall(nTransition);
      );
      
      // Continue the window
      y0 = b1 * y1 - y2;
      y2 = y1;
      y1 = y0;
      out = .5*(y0+1);
    );
    
    outL = out * lmul;
    outR = out * rmul;
    
    // Simple linear interpolation to keep CPU cost in check
    lOut = outL * ptr[];
    rOut = outR * ptr[1];
    lOut = outL * (ptr[] * (1-frac) + ptr[2]*frac);
    rOut = outR * (ptr[1] * (1-frac) + ptr[3]*frac);
    
    idx += 1;
    loc += advance;
    
    loc > .5*ptrMax ? (
      loc -= .5*(ptrMax - ptrMin);
    );
  ) : ( lOut = 0; rOut = 0; outL = 0; outR = 0; idx += 1; );
);

function granulator_init()
  instance(nextGrain, cPos)
  (
    cPos = 0;
    nextGrain = 0;
    this.grain0.idx = 
    this.grain1.idx = 
    this.grain2.idx = 
    this.grain3.idx = 
    this.grain4.idx = 
    this.grain5.idx = 
    this.grain6.idx = 
    this.grain7.idx = 
    this.grain8.idx = 
    this.grain9.idx = 
    this.grain10.idx = 
    this.grain11.idx = 
    this.grain12.idx = 
    this.grain13.idx = 
    this.grain14.idx = 100000000000000000000000000000000;
  );

function granulator_update(grainLength, speed, panSpread, reverseProbability, playing, readBufCur, readBufMin, readBufMax, writeBufCur, relPosition, pitchTimeInvariance, updateBuffer)
local(grainVar, N, ptr, pan, dist, safetyFactor, lostBuffer, total, reverse, lowLatencyCompensation, cpan, cpan_l, cpan_r)
global(GRAINDISTMIN, GRAINDISTMAX, OVERLAPMAX, VARIANCEMIN, VARIANCEMAX, srate, oversamplingMode)
instance(nextGrain, L, R, GL, GR, normalize)
(
  nextGrain -= 1;

  (playing && nextGrain <= 0) ? (
    grainLength   = .001*grainLength*srate;
  
    /* Detune */
    pitchTimeInvariance ? grainLength = grainLength * speed;
    N = floor(grainLength*.3333333333333333333333);      
    
    useAbsoluteVar ? (
      grainVar = .001 * srate * exp(VARIANCEMIN + .001*(VARIANCEMAX-VARIANCEMIN)*jitter);
    ) : (
      grainVar = .01 * jitter * grainLength;
    );
    reverse = reverseProbability > rand();      
    
    pan = 0.5 + panSpread * (rand() - 0.5);
    
    N = N/speed;
    
    useAbsoluteDist ? (
      nextGrain = .001*srate*exp(GRAINDISTMIN + .01075268*(GRAINDISTMAX-GRAINDISTMIN)*overlapOrDist);
    ) : (
      nextGrain = 3*N*(1-.01*overlapOrDist);
    );
    
    total = readBufMax - readBufMin;
    
    safetyFactor = 1/speed;
    safetyFactor < 1 ? (
      // Correct for overshooting the buffer at the front
      lowLatencyCompensation = 2 * grainLength * relPosition * safetyFactor * updateBuffer;
    ) : (
      // Correct for overshooting the buffer at the back (not actually desirable)
      1
    );
    
    grainVar = min(grainVar, .25*total);
    ptr = writeBufCur - readBufCur * total - 2*floor((rand() - (1-relPosition)) * grainVar) - 2 * grainLength * relPosition + lowLatencyCompensation - 16;
    
    // Prevent going over the edge
    ptr = min(ptr, (writeBufCur - 2 * grainLength));
    
    reverse ? (
      speed = - speed;
      ptr = ptr + 4 * grainLength * ( 1 - relPosition ) * updateBuffer + 2*grainLength * (1-updateBuffer);
    );
    
    cpan = 0.5 * $pi * pan;
    cpan_l = cos(cpan);
    cpan_r = sin(cpan);
    
    this.grain0.initGrain(ptr, N, N, readBufMin, readBufMax, speed, cpan_l, cpan_r) ? ( 0 ) 
    : this.grain1.initGrain(ptr, N, N, readBufMin, readBufMax, speed, cpan_l, cpan_r) ? ( 0 ) 
    : this.grain2.initGrain(ptr, N, N, readBufMin, readBufMax, speed, cpan_l, cpan_r) ? ( 0 ) 
    : this.grain3.initGrain(ptr, N, N, readBufMin, readBufMax, speed, cpan_l, cpan_r) ? ( 0 ) 
    : this.grain4.initGrain(ptr, N, N, readBufMin, readBufMax, speed, cpan_l, cpan_r) ? ( 0 ) 
    : this.grain5.initGrain(ptr, N, N, readBufMin, readBufMax, speed, cpan_l, cpan_r) ? ( 0 ) 
    : this.grain6.initGrain(ptr, N, N, readBufMin, readBufMax, speed, cpan_l, cpan_r) ? ( 0 ) 
    : this.grain7.initGrain(ptr, N, N, readBufMin, readBufMax, speed, cpan_l, cpan_r) ? ( 0 ) 
    : this.grain8.initGrain(ptr, N, N, readBufMin, readBufMax, speed, cpan_l, cpan_r) ? ( 0 ) 
    : this.grain9.initGrain(ptr, N, N, readBufMin, readBufMax, speed, cpan_l, cpan_r) ? ( 0 ) 
    : this.grain10.initGrain(ptr, N, N, readBufMin, readBufMax, speed, cpan_l, cpan_r) ? ( 0 )
    : this.grain11.initGrain(ptr, N, N, readBufMin, readBufMax, speed, cpan_l, cpan_r) ? ( 0 )
    : this.grain12.initGrain(ptr, N, N, readBufMin, readBufMax, speed, cpan_l, cpan_r) ? ( 0 )
    : this.grain13.initGrain(ptr, N, N, readBufMin, readBufMax, speed, cpan_l, cpan_r) ? ( 0 )
    : this.grain14.initGrain(ptr, N, N, readBufMin, readBufMax, speed, cpan_l, cpan_r) ? ( 0 )
  );
  
  this.grain0.grainRead();
  this.grain1.grainRead();
  this.grain2.grainRead();
  this.grain3.grainRead();
  /*this.grain4.grainRead();
  this.grain5.grainRead();
  this.grain6.grainRead();
  this.grain7.grainRead();
  this.grain8.grainRead();
  this.grain9.grainRead();
  this.grain10.grainRead();
  this.grain11.grainRead();
  this.grain12.grainRead();
  this.grain13.grainRead();
  this.grain14.grainRead();*/

  L = this.grain0.lOut + this.grain1.lOut + this.grain2.lOut + this.grain3.lOut + this.grain4.lOut + this.grain5.lOut + this.grain6.lOut + this.grain7.lOut + this.grain8.lOut + this.grain9.lOut + this.grain10.lOut  + this.grain11.lOut;  + this.grain12.lOut;  + this.grain13.lOut;  + this.grain14.lOut;
  R = this.grain0.rOut + this.grain1.rOut + this.grain2.rOut + this.grain3.rOut + this.grain4.rOut + this.grain5.rOut + this.grain6.rOut + this.grain7.rOut + this.grain8.rOut + this.grain9.rOut + this.grain10.rOut  + this.grain11.rOut   + this.grain12.rOut   + this.grain13.rOut   + this.grain14.rOut;

  //L = this.grain0.lOut + this.grain1.lOut + this.grain2.lOut + this.grain3.lOut + this.grain4.lOut + this.grain5.lOut + this.grain6.lOut + this.grain7.lOut + this.grain8.lOut + this.grain9.lOut + this.grain10.lOut  + this.grain11.lOut;  + this.grain12.lOut;  + this.grain13.lOut;  + this.grain14.lOut;
  //R = this.grain0.rOut + this.grain1.rOut + this.grain2.rOut + this.grain3.rOut + this.grain4.rOut + this.grain5.rOut + this.grain6.rOut + this.grain7.rOut + this.grain8.rOut + this.grain9.rOut + this.grain10.rOut  + this.grain11.rOut   + this.grain12.rOut   + this.grain13.rOut   + this.grain14.rOut;

  normalize ? (
    GL = this.grain0.outL + this.grain1.outL + this.grain2.outL + this.grain3.outL + this.grain4.outL + this.grain5.outL + this.grain6.outL + this.grain7.outL + this.grain8.outL + this.grain9.outL + this.grain10.outL + this.grain11.outL + this.grain12.outL + this.grain13.outL + this.grain14.outL;
    GR = this.grain0.outR + this.grain1.outR + this.grain2.outR + this.grain3.outR + this.grain4.outR + this.grain5.outR + this.grain6.outR + this.grain7.outR + this.grain8.outR + this.grain9.outR + this.grain10.outR + this.grain11.outR + this.grain12.outR + this.grain13.outR + this.grain14.outR;    
    L/=max(1,GL);
    R/=max(1,GR);
  );
);
