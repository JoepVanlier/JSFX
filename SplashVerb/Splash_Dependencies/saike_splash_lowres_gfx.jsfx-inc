//
//
// last_cap = mouse_cap;
// last_mouse_x = mouse_x;
// last_mouse_y = mouse_y;

@init
function pixel_rect(x, y, w, h, filled)
(
  filled == 1 ? (
    gfx_rect(x_offset + x * blit_scale, y_offset + y * blit_scale, w * blit_scale, h * blit_scale);
  ) : (
    gfx_rect(x_offset + (x + 1) * blit_scale, y_offset + y * blit_scale, (w - 2) * blit_scale, blit_scale);
    gfx_rect(x_offset + x * blit_scale, y_offset + (y + h - 1) * blit_scale, w * blit_scale, blit_scale);
    gfx_rect(x_offset + (x + w - 1) * blit_scale, y_offset + y * blit_scale, blit_scale, (h - 1) * blit_scale);
    gfx_rect(x_offset + x * blit_scale, y_offset + y * blit_scale, blit_scale, (h - 1) * blit_scale);
  );
);

function unfilled_rect(x, y, w, h)
(
  gfx_rect((x + 1), y, w - 2, 1);
  gfx_rect(x, y + h - 1, w, 1);
  gfx_rect((x + w - 1), y, 1, h - 1);
  gfx_rect(x, y, 1, h - 1);
);

function init_xy(s1, s1_min, s1_max, s1_shape, s2, s2_min, s2_max, s2_shape)
instance(
  sl1, sl2,
  sl1_min, sl2_min,
  sl1_max, sl2_max,
  sl1_shape, sl2_shape,
)
local()
global()
(
  sl1 = s1;
  sl1_min = s1_min;
  sl1_max = s1_max;
  sl1_shape = s1_shape;
  sl2 = s2;
  sl2_min = s2_min;
  sl2_max = s2_max;
  sl2_shape = s2_shape;
);

function txt_blit(str, x, y)
local(ix, row, c_char, src_x, src_y, x_start, xc, yc)
global(gfx_x)
(
  ix = 0;
  xc = x;
  yc = y;
  
  x_start = x;
  loop(strlen(str),
    c_char = str_getchar(str, ix, 'cu');
    (c_char == 10) ? (
      yc += 5;
      xc = x_start;
    ) : (
      row = floor(c_char / 16);
      src_y = 5 * row;
      src_x = 5 * (c_char - row * 16);
      
      gfx_blit(12, 1, 0, src_x, src_y, 5, 5, xc, yc, 5, 5, 0, 0); 
      xc += 5;
    );
    ix += 1;
  );
  
  gfx_x = xc;
);

function txt_measure(str)
local(ix, row, c_char, src_x, src_y, xc, yc)
global()
instance(txt_w, txt_h)
(
  ix = 0;
  xc = 0;
  yc = 0;
  loop(strlen(str),
    c_char = str_getchar(str, ix, 'cu');
    (c_char == 10) ? (
      yc += 5;
      xc = 0;
    ) : (
      row = floor(c_char / 16);
      src_y = 5 * row;
      src_x = 5 * (c_char - row * 16);
      xc += 5;
    );
    ix += 1;
  );
  
  txt_w = xc;
  txt_h = yc + 5;
);


function xy_pad(me, x, y, w, h)
instance(
  sl1, sl1_min, sl1_max,
  sl2, sl2_min, sl2_max,
  smooth_highlight,
)
local(x_val, y_val, xc, yc, x_norm, y_norm, mouse_nx, mouse_ny, over, dx, dy)
global(x_offset, y_offset, blit_scale, mouse_x, mouse_y, mouse_cap, cap_mode, last_cap, last_mouse_x, last_mouse_y)
(
  mouse_nx = (mouse_x - x_offset) / blit_scale;
  mouse_ny = (mouse_y - y_offset) / blit_scale;
  over = (mouse_nx > x) && (mouse_nx < (x + w)) && (mouse_ny > y) && (mouse_ny < (y + h));
  
  smooth_highlight = 0.7 * smooth_highlight + 0.3 * (0.03 + 0.04 * over);
  
  gfx_set(1, 1, 1, smooth_highlight);
  gfx_rect(x, y, w, h);
  unfilled_rect(x, y, w, h);
  
  x_val = slider(sl1);
  y_val = slider(sl2);
  x_norm = (x_val - sl1_min) / (sl1_max - sl1_min);
  y_norm = (y_val - sl2_min) / (sl2_max - sl2_min);
  
  xc = x + x_norm * (w - 3) + 1;
  yc = y + (1.0 - y_norm) * (h - 3) + 1;
  gfx_rect(xc, y + 1, 1, h - 2);
  gfx_rect(x + 1, yc, w - 2, 1);
  
  (over && (mouse_cap & 1) && (cap_mode == 0) && (last_cap == 0)) ? (
    cap_mode = me;
  );
  
  (cap_mode == me) ? (
    (mouse_cap == 0) ? (
      cap_mode = 0;
    ) : (
      dx = (mouse_x - last_mouse_x) / (blit_scale * w);
      dy = - (mouse_y - last_mouse_y) / (blit_scale * h);
      x_norm = max(0, min(1, x_norm + dx));
      y_norm = max(0, min(1, y_norm + dy));
      
      slider(sl1) = sl1_min + x_norm * (sl1_max - sl1_min);
      slider(sl2) = sl2_min + y_norm * (sl2_max - sl2_min);
    );
  );
);


function go_to_pixel(x, y)
(
  gfx_x = x_offset + x * blit_scale;
  gfx_y = y_offset + y * blit_scale;
);


// last_cap = mouse_cap;
// last_mouse_x = mouse_x;
// last_mouse_y = mouse_y;

function set_hint(hint)
global(gfx_x, gfx_y, mouse_x, mouse_y, mouse_cap)
local()
instance(lx, ly, lc, hint_time, current_hint, delta_time)
(
  ( (lc != mouse_cap) || ( abs(lx - mouse_x) + abs(ly - mouse_y)) > 0 ) ? (
    hint_time = 0;
  ) : (      
    (hint != 0) ? (
      current_hint = hint;
      hint_time = hint_time + delta_time;
      hint_time = min(1, hint_time)
    ) : (
      0
    )
  );
  
  lx = mouse_x;
  ly = mouse_y;
  lc = mouse_cap;
);


function draw_hints()
global(
  gfx_x, gfx_y, mouse_x, mouse_y, img_w, img_h,
  x_offset, y_offset, blit_scale,
)
local(w, h)
instance(hint_time, current_hint, last_global_time, global_time, delta_time, txt_w, txt_h)
(
  this.set_hint(0);

  global_time = time_precise();
  delta_time = global_time - last_global_time;
  last_global_time = global_time;

  ( hint_time > .99 ) ? (
    gfx_x = (mouse_x - x_offset + 15) / blit_scale;
    gfx_y = (mouse_y - y_offset + 15) / blit_scale;
    
    this.txt_measure(current_hint);
    
    ( gfx_x > 0.5 * img_w ) ? gfx_x = mouse_x - txt_w - 8;
    ( gfx_y > 0.5 * img_h ) ? gfx_y = mouse_y - txt_h - 8;

    gfx_set(0.05, 0.05, 0.1, 1.0);
    gfx_rect(gfx_x - 1, gfx_y - 1, txt_w + 2, txt_h + 1);
    gfx_set(0.3, 0.3, 0.3, 0.3);
    txt_blit(current_hint, gfx_x, gfx_y);
  );
);

function choice_toggle(x, y, w, select, slider_idx, hint)
instance(intensity)
local(current, mouse_nx, mouse_ny, over)
global(
  gfx_r, gfx_g, gfx_b, gfx_a, gfx_x mouse_cap, last_cap, mouse_x, mouse_y,
  x_offset, y_offset, blit_scale,
  hinter.set_hint,
)
(
  current = slider(slider_idx);
  mouse_nx = (mouse_x - x_offset) / blit_scale;
  mouse_ny = (mouse_y - y_offset) / blit_scale;
  over = (mouse_nx > x) && (mouse_nx < (x + w)) && (mouse_ny > y) && (mouse_ny < (y + w));

  (over && hint) ? (
    hinter.set_hint(hint);
  );

  intensity = 0.2 * (0.2 + 0.15 * over) + 0.8 * intensity;
  gfx_r = gfx_g = gfx_b = intensity; gfx_a = 0.8;
  (select == current) ? (
    gfx_rect(x, y, w, w);
  ) : (
    unfilled_rect(x, y, w, w);
  );
  gfx_x += 5;
  
  ((last_cap == 0) && (mouse_cap == 1) && over) ? (
    slider_automate(slider(slider_idx) = select);
  );
);

