@init
DBL_CLICK_SPEED = 0.2;

function colormodel(x)
global(gfx_a)
local(frac)
(
    (x < 0.25) ? ( frac = (4 * x - 0); gfx_set(0.34901960784313724 + frac * 0.20000000000000007, 0.403921568627451 + frac * -0.09019607843137256, 1.0 + frac * 0.0); )
    : (x < 0.5) ? ( frac = (4 * x - 1); gfx_set(0.5490196078431373 + frac * 0.4509803921568627, 0.3137254901960784 + frac * -0.07450980392156861, 1.0 + frac * -0.21568627450980393); )
    : (x < 0.75) ? ( frac = (4 * x - 2); gfx_set(1.0 + frac * 0.0, 0.23921568627450981 + frac * 0.10980392156862742, 0.7843137254901961 + frac * -0.6196078431372549); )
    : ( frac = (4 * x - 3); gfx_set(1.0 + frac * 0.0, 0.34901960784313724 + frac * 0.34509803921568627, 0.16470588235294117 + frac * -0.03529411764705881); )

  //x = sqrt(x); gfx_set(1.0 * x, 0.84 * x + 0.44, 1.0 - x, 0.8);
);


// Really dumb sorting algorithm which sorts two lists while shifting duplicate x coordinates
function sort_spline(n_points, xloc, yloc, indices, temp_x, temp_y)
local(idx, eps, change, tmp, shift)
global()
( 
  // First copy it to temp and set up untransformed index array
  memcpy(temp_x, xloc, n_points);
  memcpy(temp_y, yloc, n_points);
  idx = 0;
  loop(n_points,
    indices[idx] = idx;
    idx += 1;
  );
  
  // Simple bubble sort
  change = 1;
  while(change) (
    change = 0;
    idx = 0;
    loop(n_points - 1,
      (temp_x[idx] > temp_x[idx + 1]) ? (
        // Swap elements
        tmp = temp_x[idx];
        temp_x[idx] = temp_x[idx + 1];
        temp_x[idx + 1] = tmp;
        tmp = temp_y[idx];
        temp_y[idx] = temp_y[idx + 1];
        temp_y[idx + 1] = tmp;
        tmp = indices[idx];
        indices[idx] = indices[idx + 1];
        indices[idx + 1] = tmp;
        
        change = 1; // Need another pass
      );
      idx += 1;
    );
  );
  
  // Shift duplicates
  eps = 0.00001;
  idx = 0;
  shift = 0.00001;
  loop(n_points,
    (abs(temp_x[idx + 1] - temp_x[idx]) < eps) ? (
      temp_x[idx + 1] += shift;
    );
    idx += 1;
  );
  
  memcpy(xloc, temp_x, n_points);
  memcpy(yloc, temp_y, n_points);
);

function update_spline(n_points, xloc, yloc, spline_mem, temp_x, temp_y)
local(dx, dydx, tmp, idx,
      dydx_next, dydx_current,
      dx_current, dx_next, avg_dx,
      inv_dx,
      c1_current, cm,
      c1, c2, c3)
global()
(
  // Initialize coeffs
  memset(spline_mem, 0, n_points * 3);
  c1 = spline_mem;
  c2 = spline_mem + n_points;
  c3 = spline_mem + n_points * 2;

  // Calculate differences
  dx = temp_x;
  dydx = temp_y;
  
  idx = 0;
  loop(n_points - 1,
    dx[idx] = tmp = xloc[idx + 1] - xloc[idx];
    dydx[idx] = (yloc[idx + 1] - yloc[idx]) / tmp;
    idx += 1;
  );
  
  // Make sure the last node does not go bonkers
  //ptr_to_Y -= 1;
  //ptr_to_Y[] = max(min(5, ptr_to_Y[]), -5);
  
  // Calculate first degree coefficients
  c1[] = dydx[];
  idx = 0;
  loop(n_points - 2,
    dydx_current = dydx[idx];
    dydx_next = dydx[idx + 1];
    
    ( dydx_current * dydx_next <= 0 ) ? ( c1[idx + 1] = 0; ):
    (
      dx_current = dx[idx];
      dx_next = dx[idx + 1];
      avg_dx = dx_current + dx_next;
      
      c1[idx + 1] = 3.0 * avg_dx / ((avg_dx + dx_next) / dydx_current + (avg_dx + dx_current) / dydx_next);
    );
    
    idx += 1;
  );
  c1[idx + 1] = dydx[idx];
  
  // Calculate second degree coefficients
  idx = 0;
  loop(n_points - 1,
    inv_dx = 1 / dx[idx];
    c1_current = c1[idx];
    dydx_current = dydx[idx];
    cm = c1_current + c1[idx + 1] - dydx_current - dydx_current;
    c2[idx] = (dydx_current - c1_current - cm) * inv_dx;
    c3[idx] = cm * inv_dx * inv_dx;
    
    idx += 1;
  );
);

function update_spline()
instance(xloc, yloc, temp_x, temp_y, spline_mem, indices, n_points)
local(tmp)
global()
(
  this.sort_spline(n_points, xloc, yloc, indices, temp_x, temp_y);
  this.update_spline(n_points, xloc, yloc, spline_mem, temp_x, temp_y);
);

function read_from_sliders()
local(idx)
instance(x_sliders, y_sliders, start_slider, max_nodes)
global()
(
  idx = 0;
  loop(max_nodes,
    x_sliders[idx] = slider(start_slider + 2 * idx);
    y_sliders[idx] = slider(start_slider + 2 * idx + 1);
    idx += 1;
  );
);

function write_to_sliders()
local(idx)
instance(x_sliders, y_sliders, start_slider, max_nodes)
global()
(
  idx = 0;
  loop(max_nodes,
    (slider(start_slider + 2 * idx) != x_sliders[idx]) ? (
      slider_automate(slider(start_slider + 2 * idx) = x_sliders[idx]);
    );
    (slider(start_slider + 2 * idx + 1) != y_sliders[idx]) ? (
      slider_automate(slider(start_slider + 2 * idx + 1) = y_sliders[idx]);
    );
    idx += 1;
  );
);

function read_data()
local(idx)
instance(x_sliders, y_sliders, max_nodes, xloc, yloc, busy)
global()
(
  // If we are reading to/from sliders, do so here.
  this.read_from_sliders();

  idx = 0;
  loop(max_nodes,
    xloc[idx] = x_sliders[idx];
    yloc[idx] = y_sliders[idx];
    idx += 1;
  );
  
  this.update_spline();
);

function push_data()
local()
global()
instance(data_pushed)
(
  this.write_to_sliders();
  data_pushed = 1;
);

function eval_spline(x)
instance(xloc, yloc, spline_mem, n_points)
local(c1, c2, c3, diff, diff_sq, idx, x_ptr)
global()
(
  c1 = spline_mem;
  c2 = spline_mem + n_points;
  c3 = spline_mem + n_points * 2;

  idx = -1;
  x_ptr = xloc - 1;
  
  while(
    idx += 1;
    x_ptr += 1;
    
    x >= x_ptr[] && (idx < n_points)
  );
  
  idx = max(0, idx - 1);
  
  diff = x - xloc[idx];
  diff_sq = diff * diff;
        
  yloc[idx] + c1[idx] * diff + c2[idx] * diff_sq + c3[idx] * diff * diff_sq
);

/* Duplication to avoid if-statement :( */
function eval_spline_gfx(x)
instance(xloc_gfx, yloc_gfx, spline_mem_gfx, n_points_gfx)
local(c1, c2, c3, diff, diff_sq, idx, x_ptr)
global()
(
  c1 = spline_mem_gfx;
  c2 = spline_mem_gfx + n_points_gfx;
  c3 = spline_mem_gfx + n_points_gfx * 2;

  idx = -1;
  x_ptr = xloc_gfx - 1;
  
  while(
    idx += 1;
    x_ptr += 1;
    
    x >= x_ptr[] && (idx < n_points_gfx)
  );
  
  idx = max(0, idx - 1);
  
  diff = x - xloc_gfx[idx];
  diff_sq = diff * diff;
        
  yloc_gfx[idx] + c1[idx] * diff + c2[idx] * diff_sq + c3[idx] * diff * diff_sq
);

function spline_block()
local()
global()
instance(
  gfx_locked, spline_mem, spline_mem_gfx, xloc_gfx, yloc_gfx, xloc, yloc, n_points_gfx, n_points,
  data_pushed, /* Was data pushed? Update the spline */
)
(
  !gfx_locked ? (
    memcpy(spline_mem_gfx, spline_mem, 3 * n_points);
    memcpy(xloc_gfx, xloc, n_points);
    memcpy(yloc_gfx, yloc, n_points);
    n_points_gfx = n_points;
  );
  
  data_pushed ? this.read_data();
);

function handle_mouse(x, y, w, h)
local(min_idx, idx, min_dist, new_dist, dx, dy, prev_slider, next_slider, new_time, current_slider)
global(mouse_x, mouse_y, mouse_cap, mouse_wheel, DBL_CLICK_SPEED)
instance(x_sliders, y_sliders, xloc, yloc, indices, n_points, start_slider, grabbed, over, anim, last_cap, last_x, last_y, should_update, max_nodes, last_click)
(
  idx = 0;
  min_dist = 10000000000000;

  loop(n_points,
    dx = (x + w * xloc[idx] - mouse_x);
    dy = (y + h * (1.0 - yloc[idx]) - mouse_y);
    new_dist = dx * dx + dy * dy;
    (new_dist < min_dist) ? (
      min_idx = idx;
      min_dist = new_dist;
    );
    idx += 1;
  );
  
  grabbed ? (
    (mouse_cap == 1) ? (
      (grabbed >= 1) ? (
        should_update = 1;
        x_sliders[grabbed - 1] = min(1.0, max(0.0, x_sliders[grabbed - 1] + (mouse_x - last_x) / w));
        y_sliders[grabbed - 1] = min(0.5, max(-0.5, y_sliders[grabbed - 1] - (mouse_y - last_y) / h));
        this.push_data();
        anim += 1;
      );
    ) : (
      grabbed = 0;
    );
  ) : (
    (min_dist < 40) ? (
      over = min_idx;
      anim += 1;
      (last_cap == 0) && (mouse_cap == 1) ? (
        new_time = time_precise();
        (new_time - last_click) < DBL_CLICK_SPEED ? (
          y_sliders[indices[min_idx]] = 0;
          this.push_data();
        ) : (
          last_click = new_time;
          // Grab handle
          grabbed = indices[min_idx] + 1;
        );
      );
      
      (last_cap == 0) && (mouse_cap == 2) ? (
        idx = indices[min_idx];
        (idx >= 0) ? (
          x_sliders[idx] = x_sliders[n_points - 1];
          y_sliders[idx] = y_sliders[n_points - 1];
          n_points -= 1;
          this.push_data();
        );
      );
      
      (mouse_wheel != 0) ? (
        current_slider = indices[min_idx];
        next_slider = indices[min_idx + 1];
        prev_slider = indices[min_idx - 1];
        
        (next_slider < n_points) && (prev_slider >= 0) ? (
          should_update = 1;
          x_sliders[next_slider] = min(1, max(x_sliders[current_slider] + 0.001, x_sliders[next_slider] - 0.0001 * mouse_wheel));
          x_sliders[prev_slider] = min(x_sliders[current_slider] - 0.001, max(0, x_sliders[prev_slider] += 0.0001 * mouse_wheel));
          this.push_data();
        );
        mouse_wheel = 0;
      );

    ) : (
      (last_cap == 0) && (mouse_cap == 2) ? (
        (n_points < max_nodes) ? (
          x_sliders[n_points] = min(1.0, max(0.0, (mouse_x - x) / w));
          y_sliders[n_points] = min(0.5, max(-0.5, 1.0 - (mouse_y - y) / h));
          n_points += 1;

          this.push_data();
        );
      );
    
      over = -1;
      anim = 0;
    );
  );
  
  last_cap = mouse_cap;
  last_x = mouse_x;
  last_y = mouse_y;
);

function draw(x, y, w, h)
local(cx, cs, ds, x_cur, y_cur, y_new, x_new, ymin, ymax, in)
global(gfx_x, gfx_y, gfx_a)
instance(xloc, yloc, n_points, over, anim, next_in, gfx_locked)
(
  this.handle_mouse(x, y, w, h);
  
  gfx_locked = 1;
  gfx_x = x;
  gfx_y = y + h * (1.0 - this.eval_spline_gfx(0));
  cx = 0;
  cs = 0;
  ds = 1 / w;
  x_cur = x + cx;
  y_cur = y + h * (1.0 - this.eval_spline_gfx(cs));
  ymin = y + 0.5 * h;
  ymax = y + 1.5 * h;
  colormodel(1.0);
  loop(w,
    x_new = x + cx;
    y_new = y + h * (1.0 - this.eval_spline_gfx(cs));
    next_in = (y_new < ymax) && (y_new > ymin);

    y_new = min(max(y_new, ymin), ymax);
    y_cur = min(max(y_cur, ymin), ymax);
    in || next_in ? (
      gfx_a = 0.8;
      gfx_line(x_cur, y_cur, x_new, y_new);
      gfx_line(x_cur + 1, y_cur, x_new + 1, y_new);
      gfx_line(x_cur, y_cur + 1, x_new, y_new + 1);
    );
    gfx_a = 0.25;
    gfx_line(x_cur, y + h, x_new, y_cur);
    
    in = next_in;
    cx += 1;
    cs += ds;
    x_cur = x_new;
    y_cur = y_new;
  );
  
  cx = 0;
  loop(n_points,
    (cx == over) ? (
      colormodel(0.1 + 0.05 * abs(sin(0.2 * anim)));
      gfx_circle(x + w * xloc[cx], y + h * (1.0 - yloc[cx]), 4 + 2 * abs(sin(0.2 * anim)), 1);
      gfx_circle(x + w * xloc[cx], y + h * (1.0 - yloc[cx]), 5 + 2 * abs(sin(0.2 * anim)), 1);
    ) : (
      colormodel(0.0);
      gfx_circle(x + w * xloc[cx], y + h * (1.0 - yloc[cx]), 5, 1);
      colormodel(0.1);
      gfx_circle(x + w * xloc[cx], y + h * (1.0 - yloc[cx]), 4, 1);
      colormodel(0.2);
      gfx_circle(x + w * xloc[cx], y + h * (1.0 - yloc[cx]), 3, 1);
    );
    cx += 1;
  );
  gfx_locked = 0;
  
  /* Debugging
  gfx_x = 0;
  gfx_y = 0;
  gfx_set(1, 1, 1, 1);
  cx = 0;
  loop(n_points,
    gfx_printf("%f ",xloc[cx]);
    cx += 1;
  );
  */
);

function init_spline(freemem, _start_slider)
local(SPLINE_SIZE, idx, dx)
global()
instance(
  x_sliders, y_sliders,
  xloc, yloc,
  xloc_gfx, yloc_gfx,
  indices,
  initialized,
  temp_x, temp_y,
  spline_mem,
  spline_mem_gfx,
  n_points,
  MAX_NODES,
  start_slider,
)
(
  n_points == 0 ? n_points = 6;
  start_slider = _start_slider;
  
  SPLINE_SIZE = 128;
  MAX_NODES = 16;
  freemem = (x_sliders = freemem) + SPLINE_SIZE;
  freemem = (y_sliders = freemem) + SPLINE_SIZE;
  
  freemem = (xloc = freemem) + SPLINE_SIZE;
  freemem = (yloc = freemem) + SPLINE_SIZE;
  freemem = (indices = freemem) + SPLINE_SIZE;
  freemem = (temp_x = freemem) + SPLINE_SIZE;
  freemem = (temp_y = freemem) + SPLINE_SIZE;
  freemem = (spline_mem = freemem) + 3 * SPLINE_SIZE;
  freemem = (spline_mem_gfx = freemem) + 3 * SPLINE_SIZE;
  freemem = (xloc_gfx = freemem) + SPLINE_SIZE;
  freemem = (yloc_gfx = freemem) + SPLINE_SIZE;
  
  // Initialize as a straight line
  this.read_data();
  this.update_spline();
);

