@init
function init_stft_mem(freemem, size)
instance(
  buffer_index, buffer_length,
  fft_buffer, window_buffer, 
  input_buffer, output_buffer,
  fft_size_index, phase_left, phase_right,
)
local(MAX_FFT_SIZE)
global()
(
  // Make sure we don't end up on a boundary
  freemem = ceil(freemem / 65536) * 65536;
  
  (fft_size_index == 0) ? (
    // Only initialize buffer position if we haven't initialized before.
    buffer_index = 0;
  );
  
  fft_size_index = size;

  MAX_FFT_SIZE = 32768;
  
  /* There's some additional padding so that we don't have to range check when octaving */
  freemem = (fft_buffer = freemem) + MAX_FFT_SIZE * 4;
  freemem = (window_buffer = freemem) + MAX_FFT_SIZE * 4;
  freemem = (phase_left = freemem) + MAX_FFT_SIZE * 2;
  freemem = (phase_right = freemem) + MAX_FFT_SIZE * 2;
  
  buffer_length = 32768 * 2;
  freemem = (input_buffer = freemem) + buffer_length * 2;
  freemem = (output_buffer = freemem) + buffer_length * 2;
  freemem
);

function block_stft()
instance(fft_buffer, fft_size, prev_fft_size, fft_interval, fft_scaling_factor, window_buffer, fft_size_index)
local(overlap_factor, i, r)
global()
(
  overlap_factor = 6;
  fft_size = 256 << fft_size_index;
  fft_interval = fft_size / overlap_factor;
  fft_scaling_factor = 1.0 / overlap_factor / fft_size;
  
  fft_size != prev_fft_size ? (
    prev_fft_size = fft_size;
    // Fill window buffer
    i = 0;
    loop(fft_size,
      r = (i + 0.5) / fft_size;
      window_buffer[i] = (0.5 - 0.5 * cos(2 * $pi * r)) / sqrt(0.375);
      i += 1;
    );
  );
);

function process_stft_segment(fft_buffer, fft_size, phase_left, phase_right)
global()
instance()
local(
  fft_bin, tfft_bin2, tmp, /* Iterators */
  left_real, left_imag, right_real, right_imag,
  left_real_new, left_imag_new, right_real_new, right_imag_new,
  tfft_bin,
  
  t11, t12,
  t21, t22,
  
  pl1, pl2,
  pr1, pr2,
)
(
  fft_bin = 0; // FFT bin number
  
  tfft_bin = 0;
  loop(fft_size/2,
    tfft_bin2 = fft_bin ? 2 * (fft_size - fft_bin) : 0;

    // Unfold complex spectrum into two real spectra
    t11 = fft_buffer[tfft_bin];
    t12 = fft_buffer[tfft_bin + 1];
    t21 = fft_buffer[tfft_bin2];
    t22 = fft_buffer[tfft_bin2 + 1];
    
    left_real = t11 + t21;
    left_imag = t12 - t22;
    right_real = t12 + t22;
    right_imag = -t11 + t21;
    
    // Basic phase rotation
    pl1 = phase_left[tfft_bin];
    pl2 = phase_left[tfft_bin + 1];
    pr1 = phase_right[tfft_bin];
    pr2 = phase_right[tfft_bin + 1];
    left_real_new = left_real * pl1 - left_imag * pl2;
    left_imag_new = left_real * pl2 + left_imag * pl1;
    right_real_new = right_real * pr1 - right_imag * pr2;
    right_imag_new = right_real * pr2 + right_imag * pr1;
 
    // Re-fold back into complex spectrum
    fft_buffer[tfft_bin] = (left_real_new - right_imag_new) * 0.5;
    fft_buffer[tfft_bin + 1] = (left_imag_new + right_real_new) * 0.5;
    fft_buffer[tfft_bin2] = (left_real_new + right_imag_new) * 0.5;
    fft_buffer[tfft_bin2 + 1] = (-left_imag_new + right_real_new) * 0.5;

    fft_bin += 1;
    tfft_bin += 2;
  );
);

function fft_input(input_buffer, fft_buffer, window_buffer, fft_size, buffer_index, buffer_length)
local(bi, i, i2, it)
global()
(
  // Copy input to buffer
  bi = buffer_index - fft_size + 1;
  i = 0;
  loop(fft_size,
    i2 = bi + i;
    i2 < 0 ? i2 += buffer_length;
  
    fft_buffer[2 * i] = input_buffer[2 * i2] * window_buffer[i];
    fft_buffer[2 * i + 1] = input_buffer[2 * i2 + 1] * window_buffer[i];
  
    i += 1;
  );
  
  // Process buffer
  fft(fft_buffer, fft_size);
  fft_permute(fft_buffer, fft_size);
);

function process_stft(in0, in1)
local(i, bi, i2, inv_size, sc, fa, xc, eval, out_ix)
global(spline, spline.should_update, spline.eval_spline, srate, scale, polarity)
instance(
  buffer_ready,
  input_buffer,
  output_buffer,
  buffer_index,
  output_index,
  fft_counter, fft_interval,
  fft_buffer,
  buffer_length,
  window_buffer,
  fft_size,
  fft_scaling_factor,
  out_left,
  out_right,
  phase_left,
  phase_right,
  )
(
  input_buffer[buffer_index * 2] = in0;
  input_buffer[buffer_index * 2 + 1] = in1;
  
  buffer_ready > 0 ? (
    buffer_ready += 1;
  );
   
  // Once a block is ready, begin processing.
  fft_counter += 1;
  fft_counter >= fft_interval ? (
    fft_counter = 0;
    fft_input(input_buffer, fft_buffer, window_buffer, fft_size, buffer_index, buffer_length);
    buffer_ready = 1;
    
    fa = 20 / srate;
    
    spline.should_update ? (
      i = 0; i2 = 0; inv_size = 1.0 / fft_size;
      sc = 2.0 * scale * $pi;
      loop(fft_size,
        xc = (log10(i2) - log10(fa)) / (log10(0.5) - log10(fa));
        eval = sc * spline.eval_spline(xc);
        phase_right[i] = cos(eval);
        phase_right[i + 1] = - sin(eval);
        (polarity > 0) ? (
          phase_left[i] = cos(- eval);
          phase_left[i + 1] = - sin(- eval);
        ) : (
          phase_left[i] = phase_right[i];
          phase_left[i + 1] = phase_right[i + 1];
        );
        i += 2;
        i2 += inv_size;
      );
      phase_right[0] = 1;
      phase_right[1] = 0;
      phase_left[0] = 1;
      phase_left[1] = 0;
      spline.should_update = 0;
    );
    
    this.process_stft_segment(fft_buffer, fft_size, phase_left, phase_right);
    fft_ipermute(fft_buffer, fft_size);
    ifft(fft_buffer, fft_size);
    
    // Add to output
    bi = buffer_index - fft_size + 1;
    i = 0;
    loop(fft_size,
      i2 = bi + i;
      (i2 < 0) ? i2 += buffer_length;
      output_buffer[2 * i2] += fft_buffer[2 * i] * window_buffer[i] * fft_scaling_factor;
      output_buffer[2 * i2 + 1] += fft_buffer[2 * i + 1] * window_buffer[i] * fft_scaling_factor;
      i += 1;
    );
    buffer_ready = 0;
    
  );
  
  output_index = buffer_index - fft_size;
  output_index < 0 ? output_index += buffer_length;
  
  out_ix = output_index * 2;
  out_left = output_buffer[out_ix];
  out_right = output_buffer[out_ix + 1];
  output_buffer[out_ix] = 0;
  output_buffer[out_ix + 1] = 0;
  
  buffer_index = (buffer_index + 1) % buffer_length;
);

function allocate_stereo_delay_mem(freemem, size)
instance(delay_mem, delay_end, ptr)
local()
global()
(
  (delay_end != (freemem + size)) ? (
    // Reinitialize if the memory mapping changed.
    memset(freemem, 0, size);
    ptr = freemem;
    delay_mem = freemem;
    delay_end = freemem + size;
  );
  ptr < delay_mem ? ptr = delay_mem;
  
  freemem + size
);

function simple_stereo_delay(in_l, in_r, read_offset)
local(read_ptr)
instance(delay_mem, delay_end, ptr, l, r)
global(max_read, max_write)
(
  ptr[] = in_l;
  ptr[1] = in_r;
  
  read_ptr = ptr - 2 * read_offset;
  read_ptr < delay_mem ? read_ptr += (delay_end - delay_mem);
  l = read_ptr[];
  r = read_ptr[1];
  
  max_read = min(max_read, read_ptr);
  max_write = min(max_write, ptr);
  
  ptr += 2;
  (ptr >= delay_end) ? ptr = delay_mem;
);
